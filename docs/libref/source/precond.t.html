<html><head><link rel=stylesheet type="text/css" href="../libref.css"><title>precond.t</title></head><body>
<table class=ban><tr><td><h1>precond.t</h1><td align=right><a href="../file/precond.t.html">documentation</a></table><pre>
<a name="1"></a>#charset "us-ascii"
<a name="2"></a>#include "advlite.h"
<a name="3"></a>
<a name="4"></a>
<a name="5"></a>/*
<a name="6"></a> *   ***************************************************************************
<a name="7"></a> *   precond.t
<a name="8"></a> *
<a name="9"></a> *   This module forms part of the adv3Lite library (c) 2012-13 Eric Eve
<a name="10"></a> *
<a name="11"></a> *   Based on code in the adv3 library (c) Michael J. Roberts.
<a name="12"></a> */
<a name="13"></a>
<a name="14"></a>/* 
<a name="15"></a> *   A PreCondition encapsulate a condition that must be fulfilled in order for
<a name="16"></a> *   an action to be fulfilled (e.g. a container must be open before we can put
<a name="17"></a> *   anything in it). A PreCondition may also try to bring about the fulfilment
<a name="18"></a> *   of the condition it enforces via an implicit action.
<a name="19"></a> */
<a name="20"></a>class PreCondition: object
<a name="21"></a>    /*
<a name="22"></a>     *   Check the condition on the given object (which may be nil, if
<a name="23"></a>     *   this condition doesn't apply specifically to one of the objects
<a name="24"></a>     *   in the command).  If it is possible to meet the condition with an
<a name="25"></a>     *   implicit command, and allowImplicit is true, try to execute the
<a name="26"></a>     *   command.  If the condition cannot be met, report a failure and
<a name="27"></a>     *   return nil to terminate the command.
<a name="28"></a>     *   
<a name="29"></a>     *   If allowImplicit is nil, an implicit command may not be
<a name="30"></a>     *   attempted.  In this case, if the condition is not met, we must
<a name="31"></a>     *   simply report a failure return nil to terminate the command.
<a name="32"></a>     */
<a name="33"></a>    checkPreCondition(obj, allowImplicit) { return true; }
<a name="34"></a>
<a name="35"></a>    /*
<a name="36"></a>     *   Verify the condition.  This is called during the object
<a name="37"></a>     *   verification step so that the pre-condition can add verifications
<a name="38"></a>     *   of its own.  This can be used, for example, to add likelihood to
<a name="39"></a>     *   objects that already meet the condition.  Note that it is
<a name="40"></a>     *   generally not desirable to report illogical for conditions that
<a name="41"></a>     *   checkPreCondition() enforces, because doing so will prevent
<a name="42"></a>     *   checkPreCondition() from ever being reached and thus will prevent
<a name="43"></a>     *   checkPreCondition() from attempting to carry out implicit actions
<a name="44"></a>     *   to meet the condition.
<a name="45"></a>     *   
<a name="46"></a>     *   'obj' is the object being checked.  Note that because this is
<a name="47"></a>     *   called during verification, the explicitly passed-in object must
<a name="48"></a>     *   be used in the check rather than the current object in the global
<a name="49"></a>     *   current action.  
<a name="50"></a>     */
<a name="51"></a>    verifyPreCondition(obj) { }
<a name="52"></a>
<a name="53"></a>    /*
<a name="54"></a>     *   Precondition execution order.  When we execute preconditions for a
<a name="55"></a>     *   given action, we'll sort the list of all applicable preconditions
<a name="56"></a>     *   in ascending execution order.
<a name="57"></a>     *   
<a name="58"></a>     *   For the most part, the relative order of two preconditions is
<a name="59"></a>     *   arbitrary.  In some unusual cases, though, the order is important,
<a name="60"></a>     *   such as when applying one precondition can destroy the conditions
<a name="61"></a>     *   that the other would try to create but not vice versa.  When the
<a name="62"></a>     *   order doesn't matter, this can be left at the default setting.  
<a name="63"></a>     */
<a name="64"></a>    preCondOrder = 100
<a name="65"></a>;
<a name="66"></a>    
<a name="67"></a>/* 
<a name="68"></a> *   A precondition to check whether a container is open. If this is defined on
<a name="69"></a> *   the action handling of an object, it only takes effect if that object
<a name="70"></a> *   behaves like a container, in which case either it, or its remapIn object,
<a name="71"></a> *   must be open for the action to proceed.
<a name="72"></a> */
<a name="73"></a>containerOpen: PreCondition
<a name="74"></a>    checkPreCondition(obj, allowImplicit) 
<a name="75"></a>    { 
<a name="76"></a>        /* 
<a name="77"></a>         *   if we have a non-nil remapIn property, that's the container
<a name="78"></a>         *   representing us, so we need to use it instead.
<a name="79"></a>         */        
<a name="80"></a>        if(obj.remapIn != nil)
<a name="81"></a>            obj = obj.remapIn;
<a name="82"></a>        
<a name="83"></a>        
<a name="84"></a>        /* 
<a name="85"></a>         *   If the object is already open, we're already done; also there's
<a name="86"></a>         *   nothing to be done if we're not a container (in the sense of
<a name="87"></a>         *   something objects can be put in).
<a name="88"></a>         */               
<a name="89"></a>        if (obj == nil || obj.contType != In || obj.isOpen)
<a name="90"></a>            return true;
<a name="91"></a>        
<a name="92"></a>        /* If we're allowed to try an implicit action, then try opening obj */
<a name="93"></a>        if(allowImplicit) 
<a name="94"></a>        {
<a name="95"></a>            /* 
<a name="96"></a>             *   Try opening obj via an implicit action and note whether tha
<a name="97"></a>             *   action was actually attempted.
<a name="98"></a>             */                
<a name="99"></a>            local tried = tryImplicitAction(Open, obj);
<a name="100"></a>            
<a name="101"></a>            /* 
<a name="102"></a>             *   If obj is now open, this precondition has been met, so return
<a name="103"></a>             *   true.
<a name="104"></a>             */
<a name="105"></a>            if(obj.isOpen)
<a name="106"></a>                return true;
<a name="107"></a>            
<a name="108"></a>            /* 
<a name="109"></a>             *   Otherwise if we tried but failed to open obj, return nil to
<a name="110"></a>             *   signal that we haven't met this precondition so the main action
<a name="111"></a>             *   can't go ahead. The attempt to open obj will have reported the
<a name="112"></a>             *   reason why it failed, so there's no need to display anything
<a name="113"></a>             *   else.
<a name="114"></a>             */
<a name="115"></a>            if(tried)
<a name="116"></a>                return nil;
<a name="117"></a>        }
<a name="118"></a>        
<a name="119"></a>        /* 
<a name="120"></a>         *   If we reach here obj is closed and we weren't allowed to try to
<a name="121"></a>         *   open it; display a message explaining the problem.
<a name="122"></a>         */
<a name="123"></a>        gMessageParams(obj);
<a name="124"></a>        DMsg(container needs to be open, '{The subj obj} need{s/ed} to be open
<a name="125"></a>            for that. ');
<a name="126"></a>        
<a name="127"></a>        /* Then return nil to indicate that the precondition hasn't been met. */
<a name="128"></a>        return nil;        
<a name="129"></a>    }
<a name="130"></a>    
<a name="131"></a>;
<a name="132"></a>
<a name="133"></a>/* 
<a name="134"></a> *   A precondition to check whether a container's interior is visible (for looking inside the
<a name="135"></a> *   object). If this is defined on the action handling of an object, it only takes effect if that
<a name="136"></a> *   object behaves like a container, in which case either it, or its remapIn object, must be open
<a name="137"></a> *   for the action to proceed.
<a name="138"></a> */
<a name="139"></a>
<a name="140"></a>containerInteriorVisible: containerOpen
<a name="141"></a>    checkPreCondition(obj, allowImplicit) 
<a name="142"></a>    { 
<a name="143"></a>        /* 
<a name="144"></a>         *   if we have a non-nil remapIn property, that's the container
<a name="145"></a>         *   representing us, so we need to use it instead.
<a name="146"></a>         */        
<a name="147"></a>        if(obj.remapIn != nil)
<a name="148"></a>            obj = obj.remapIn;
<a name="149"></a>        
<a name="150"></a>        /* 
<a name="151"></a>         *   If the object is transparent or the actor is inside it, thie preconidition is met, so
<a name="152"></a>         *   we return true.
<a name="153"></a>         */
<a name="154"></a>        if(obj.isTransparent || gActor.isIn(obj))
<a name="155"></a>            return true;
<a name="156"></a>        
<a name="157"></a>        /* Otherwse use the containerOpen handling. */
<a name="158"></a>        return inherited(obj, allowImplicit);    
<a name="159"></a>    }
<a name="160"></a>;
<a name="161"></a>
<a name="162"></a>/* 
<a name="163"></a> *   A precondition to check whether a container's interior is accessible (for putting something
<a name="164"></a> *   inside the object).
<a name="165"></a> *
<a name="166"></a> */
<a name="167"></a>containerInteriorAccessible: containerOpen
<a name="168"></a>    checkPreCondition(obj, allowImplicit) 
<a name="169"></a>    { 
<a name="170"></a>        /* 
<a name="171"></a>         *   if we have a non-nil remapIn property, that's the container
<a name="172"></a>         *   representing us, so we need to use it instead.
<a name="173"></a>         */        
<a name="174"></a>        if(obj.remapIn != nil)
<a name="175"></a>            obj = obj.remapIn;
<a name="176"></a>        
<a name="177"></a>        /* 
<a name="178"></a>         *   If the actor is inside us, thie preconidition is met, so
<a name="179"></a>         *   we return true.
<a name="180"></a>         */
<a name="181"></a>        if(gActor.isIn(obj))
<a name="182"></a>            return true;
<a name="183"></a>        
<a name="184"></a>        /* Otherwse use the containerOpen handling. */
<a name="185"></a>        return inherited(obj, allowImplicit);    
<a name="186"></a>    }
<a name="187"></a>;
<a name="188"></a>
<a name="189"></a>/* A PreCondition to check whether an object is open. */
<a name="190"></a>objOpen: PreCondition
<a name="191"></a>    
<a name="192"></a>    verifyPreCondition(obj)
<a name="193"></a>    {
<a name="194"></a>        /* 
<a name="195"></a>         *   If the object isn't open already, make it a slightly less logical
<a name="196"></a>         *   choice of object for this command.
<a name="197"></a>         */
<a name="198"></a>        if(!obj.isOpen)
<a name="199"></a>            logicalRank(90);
<a name="200"></a>    }
<a name="201"></a>    
<a name="202"></a>    checkPreCondition(obj, allowImplicit) 
<a name="203"></a>    { 
<a name="204"></a>        /* 
<a name="205"></a>         *   if the object is already open, we're already done.
<a name="206"></a>         */
<a name="207"></a>        if (obj == nil || obj.isOpen)
<a name="208"></a>            return true;
<a name="209"></a>        
<a name="210"></a>        /* 
<a name="211"></a>         *   If we're allowed to attempt an implicit action, try opening obj
<a name="212"></a>         *   implicitly and see if we succeed.
<a name="213"></a>         */
<a name="214"></a>        if(allowImplicit) 
<a name="215"></a>        {
<a name="216"></a>            /* 
<a name="217"></a>             *   Try opening obj implicitly and note if we were allowed to make
<a name="218"></a>             *   the attempt.
<a name="219"></a>             */
<a name="220"></a>            local tried = tryImplicitAction(Open, obj);
<a name="221"></a>            
<a name="222"></a>            /*  
<a name="223"></a>             *   If obj is now open return true to signal that this precondition
<a name="224"></a>             *   has now been met.
<a name="225"></a>             */
<a name="226"></a>            if(obj.isOpen)
<a name="227"></a>                return true;
<a name="228"></a>            
<a name="229"></a>            /* 
<a name="230"></a>             *   Otherwise, if we tried but failed to open obj, return nil to
<a name="231"></a>             *   signal that this precondition can't be met (so the main action
<a name="232"></a>             *   cannot proceed). The attempt to open obj will have explained
<a name="233"></a>             *   why it failed, so there's no need for any further explanation
<a name="234"></a>             *   here.
<a name="235"></a>             */
<a name="236"></a>            if(tried)
<a name="237"></a>                return nil;
<a name="238"></a>        }
<a name="239"></a>        
<a name="240"></a>        /* 
<a name="241"></a>         *   If we reach here obj is closed and we weren't allowed to try to
<a name="242"></a>         *   open it; display a message explaining the problem.
<a name="243"></a>         */
<a name="244"></a>        gMessageParams(obj);
<a name="245"></a>        DMsg(object needs to be open, '{The subj obj} need{s/ed} to be open for
<a name="246"></a>            that. ');
<a name="247"></a>        
<a name="248"></a>        /* Then return nil to indicate that the precondition hasn't been met. */
<a name="249"></a>        return nil;        
<a name="250"></a>    }
<a name="251"></a>    
<a name="252"></a>;
<a name="253"></a>
<a name="254"></a>
<a name="255"></a>/* A PreCondition to check whether an object is closed. */
<a name="256"></a>objClosed: PreCondition
<a name="257"></a>    
<a name="258"></a>    verifyPreCondition(obj)
<a name="259"></a>    {
<a name="260"></a>        /* 
<a name="261"></a>         *   If the object is open, make it a slightly less logical choice of
<a name="262"></a>         *   object for this command.
<a name="263"></a>         */
<a name="264"></a>        if(obj.isOpen)
<a name="265"></a>            logicalRank(90);
<a name="266"></a>    }
<a name="267"></a>    
<a name="268"></a>    
<a name="269"></a>    checkPreCondition(obj, allowImplicit) 
<a name="270"></a>    { 
<a name="271"></a>        /* 
<a name="272"></a>         *   if the object is already closed, we're already done.
<a name="273"></a>         */
<a name="274"></a>        if (obj == nil || ! obj.isOpen)
<a name="275"></a>            return true;
<a name="276"></a>        
<a name="277"></a>        
<a name="278"></a>        /* 
<a name="279"></a>         *   If we're allowed to attempt an implicit action, try closing obj
<a name="280"></a>         *   implicitly and see if we succeed.
<a name="281"></a>         */
<a name="282"></a>        if(allowImplicit) 
<a name="283"></a>        {
<a name="284"></a>            /* 
<a name="285"></a>             *   Try closing obj implicitly and note if we were allowed to make
<a name="286"></a>             *   the attempt.
<a name="287"></a>             */
<a name="288"></a>            local tried = tryImplicitAction(Close, obj);
<a name="289"></a>            
<a name="290"></a>             /*  
<a name="291"></a>              *   If obj is now closed return true to signal that this
<a name="292"></a>              *   precondition has now been met.
<a name="293"></a>              */            
<a name="294"></a>            if(!obj.isOpen)
<a name="295"></a>                return true;
<a name="296"></a>            
<a name="297"></a>            /* 
<a name="298"></a>             *   Otherwise, if we tried but failed to open obj, return nil to
<a name="299"></a>             *   signal that this precondition can't be met (so the main action
<a name="300"></a>             *   cannot proceed). The attempt to close obj will have explained
<a name="301"></a>             *   why it failed, so there's no need for any further explanation
<a name="302"></a>             *   here.
<a name="303"></a>             */
<a name="304"></a>            if(tried)
<a name="305"></a>                return nil;
<a name="306"></a>        }
<a name="307"></a>        
<a name="308"></a>        
<a name="309"></a>        /* 
<a name="310"></a>         *   If we reach here obj is open and we weren't allowed to try to
<a name="311"></a>         *   close it; display a message explaining the problem.
<a name="312"></a>         */
<a name="313"></a>        gMessageParams(obj);        
<a name="314"></a>        DMsg(obj needs to be closed, '{The subj obj} need{s/ed} to be closed for
<a name="315"></a>            that. ');
<a name="316"></a>        
<a name="317"></a>        /* Then return nil to indicate that the precondition hasn't been met. */
<a name="318"></a>        return nil;        
<a name="319"></a>    }
<a name="320"></a>    
<a name="321"></a>;  
<a name="322"></a>    
<a name="323"></a>/* 
<a name="324"></a> *   A PreCondition to check whether an object is unlocked, and to attempt to
<a name="325"></a> *   unlock it if it seems possible to do so.
<a name="326"></a> */
<a name="327"></a>objUnlocked: PreCondition
<a name="328"></a>    checkPreCondition(obj, allowImplicit) 
<a name="329"></a>    { 
<a name="330"></a>        /* 
<a name="331"></a>         *   If the object is already unlocked, we're already done. If the
<a name="332"></a>         *   object is neither lockableWithKey nor lockableWithoutKey there's
<a name="333"></a>         *   nothing we can do here.
<a name="334"></a>         */
<a name="335"></a>        if (obj == nil || ! obj.isLocked 
<a name="336"></a>            || obj.lockability not in (lockableWithoutKey, lockableWithKey) )
<a name="337"></a>            return true;
<a name="338"></a>        
<a name="339"></a>        /* 
<a name="340"></a>         *   If the object can be unlocked without a Key, try unlocking it
<a name="341"></a>         *   implicitly
<a name="342"></a>         */
<a name="343"></a>        if(allowImplicit &amp;&amp; obj.lockability == lockableWithoutKey)
<a name="344"></a>        {
<a name="345"></a>            /* 
<a name="346"></a>             *   Try unlocking obj implicitly and note if we were allowed to make
<a name="347"></a>             *   the attempt.
<a name="348"></a>             */
<a name="349"></a>            local tried = tryImplicitAction(Unlock, obj);
<a name="350"></a>            
<a name="351"></a>             /*  
<a name="352"></a>              *   If obj is now unlocked return true to signal that this
<a name="353"></a>              *   precondition has now been met.
<a name="354"></a>              */            
<a name="355"></a>            if(!obj.isLocked)
<a name="356"></a>                return true;
<a name="357"></a>            
<a name="358"></a>            /* 
<a name="359"></a>             *   Otherwise, if we tried but failed to unlock obj, return nil to
<a name="360"></a>             *   signal that this precondition can't be met (so the main action
<a name="361"></a>             *   cannot proceed). The attempt to unlock obj will have explained
<a name="362"></a>             *   why it failed, so there's no need for any further explanation
<a name="363"></a>             *   here.
<a name="364"></a>             */
<a name="365"></a>            if(tried)
<a name="366"></a>                return nil;
<a name="367"></a>        }
<a name="368"></a>        
<a name="369"></a>        
<a name="370"></a>        /* 
<a name="371"></a>         *   If the object needs a key to unlock it, attempt to unlock it with a
<a name="372"></a>         *   plausible key if there is one, otherwise return true to let the
<a name="373"></a>         *   OPEN action go ahead and fail.
<a name="374"></a>         */
<a name="375"></a>        
<a name="376"></a>        if(allowImplicit &amp;&amp; obj.lockability == lockableWithKey)
<a name="377"></a>        {
<a name="378"></a>            /* 
<a name="379"></a>             *   Try to find a key held by the actor that may unlock this
<a name="380"></a>             *   object; the result will be stored in the useKey_ property of
<a name="381"></a>             *   the object.
<a name="382"></a>             */
<a name="383"></a>            obj.findPlausibleKey(true);
<a name="384"></a>            
<a name="385"></a>            /* 
<a name="386"></a>             *   If we don't find a key, return TRUE to let the OPEN action go
<a name="387"></a>             *   ahead and report that the object is locked, since there's
<a name="388"></a>             *   nothing else we can do here.
<a name="389"></a>             */
<a name="390"></a>            if(obj.useKey_ == nil)
<a name="391"></a>                return true;
<a name="392"></a>            
<a name="393"></a>            /* 
<a name="394"></a>             *   Try unlocking obj with useKey_ implicitly and note if we were
<a name="395"></a>             *   allowed to make the attempt.
<a name="396"></a>             */
<a name="397"></a>            local tried = tryImplicitAction(UnlockWith, obj, obj.useKey_);
<a name="398"></a>            
<a name="399"></a>             /*  
<a name="400"></a>              *   If obj is now unlocked return true to signal that this
<a name="401"></a>              *   precondition has now been met.
<a name="402"></a>              */            
<a name="403"></a>            if(!obj.isLocked)
<a name="404"></a>                return true;
<a name="405"></a>            
<a name="406"></a>            /* 
<a name="407"></a>             *   Otherwise, if we tried but failed to unlock obj, return nil to
<a name="408"></a>             *   signal that this precondition can't be met (so the main action
<a name="409"></a>             *   cannot proceed). The attempt to unlock obj will have explained
<a name="410"></a>             *   why it failed, so there's no need for any further explanation
<a name="411"></a>             *   here.
<a name="412"></a>             */
<a name="413"></a>            if(tried)
<a name="414"></a>                return nil;
<a name="415"></a>        }
<a name="416"></a>        
<a name="417"></a>        /* 
<a name="418"></a>         *   Although the PreCondition hasn't been met, we'll return true at
<a name="419"></a>         *   this point to let the OPEN action go ahead and report the problem.
<a name="420"></a>         */
<a name="421"></a>        
<a name="422"></a>        return true;
<a name="423"></a>    }
<a name="424"></a>;
<a name="425"></a>
<a name="426"></a>    
<a name="427"></a>/* A PreCondition to check whether an object is currently held by the actor. */
<a name="428"></a>objHeld: PreCondition
<a name="429"></a>    
<a name="430"></a>    verifyPreCondition(obj)
<a name="431"></a>    {
<a name="432"></a>        /* 
<a name="433"></a>         *   If the object is fixed in place it can't be picked up, so there's
<a name="434"></a>         *   no point in trying. BUT we also have to check for the case that the
<a name="435"></a>         *   object is directly in the player (perhaps because a body part).
<a name="436"></a>         */        
<a name="437"></a>        if(obj.isFixed &amp;&amp; !obj.isDirectlyIn(gActor))
<a name="438"></a>            illogical(obj.cannotTakeMsg);
<a name="439"></a>        
<a name="440"></a>        
<a name="441"></a>        /* 
<a name="442"></a>         *   If the actor isn't carrying the object it's slightly less likely to
<a name="443"></a>         *   be the one the player means.
<a name="444"></a>         */        
<a name="445"></a>        if(!obj.isIn(gActor))
<a name="446"></a>            logicalRank(90);
<a name="447"></a>        
<a name="448"></a>    }
<a name="449"></a>    
<a name="450"></a>    checkPreCondition(obj, allowImplicit) 
<a name="451"></a>    { 
<a name="452"></a>        /* 
<a name="453"></a>         *   if the object is already held, we're already done.
<a name="454"></a>         */
<a name="455"></a>        if (obj == nil || obj.isDirectlyIn(gActor))
<a name="456"></a>            return true;
<a name="457"></a>        
<a name="458"></a>        
<a name="459"></a>        /* 
<a name="460"></a>         *   If we're allowed to attempt an implicit action, try taking obj
<a name="461"></a>         *   implicitly and see if we succeed.
<a name="462"></a>         */
<a name="463"></a>        if(allowImplicit) 
<a name="464"></a>        {    
<a name="465"></a>            /* 
<a name="466"></a>             *   Try taking obj implicitly and note if we were allowed to make
<a name="467"></a>             *   the attempt.
<a name="468"></a>             */
<a name="469"></a>            local tried = tryImplicitAction(Take, obj);
<a name="470"></a>            
<a name="471"></a>            /*  
<a name="472"></a>             *   If obj is now held by the actor return true to signal that this
<a name="473"></a>             *   precondition has now been met.
<a name="474"></a>             */
<a name="475"></a>            if(obj.isDirectlyIn(gActor))
<a name="476"></a>              return true;   
<a name="477"></a>            
<a name="478"></a>            /* 
<a name="479"></a>             *   Otherwise, if we tried but failed to take obj, return nil to
<a name="480"></a>             *   signal that this precondition can't be met (so the main action
<a name="481"></a>             *   cannot proceed). The attempt to take obj will have explained
<a name="482"></a>             *   why it failed, so there's no need for any further explanation
<a name="483"></a>             *   here.
<a name="484"></a>             */
<a name="485"></a>            if(tried)
<a name="486"></a>                return nil;            
<a name="487"></a>        }
<a name="488"></a>        
<a name="489"></a>        /* 
<a name="490"></a>         *   If we reach here obj isn't being held by the actor and we weren't
<a name="491"></a>         *   allowed to try to take it; display a message explaining the
<a name="492"></a>         *   problem.
<a name="493"></a>         */
<a name="494"></a>        gMessageParams(obj);
<a name="495"></a>        DMsg(need to hold, '{I} need{s/ed} to be holding {the obj} to do that. ');
<a name="496"></a>        
<a name="497"></a>        /* Then return nil to indicate that the precondition hasn't been met. */
<a name="498"></a>        return nil;        
<a name="499"></a>    }
<a name="500"></a>;
<a name="501"></a>
<a name="502"></a>/* A PreCondition to check that an object isn't being worn. */
<a name="503"></a>objNotWorn: PreCondition
<a name="504"></a>    checkPreCondition(obj, allowImplicit) 
<a name="505"></a>    { 
<a name="506"></a>        /* 
<a name="507"></a>         *   if the object is not being worn, we're already done.
<a name="508"></a>         */
<a name="509"></a>        if (obj == nil || obj.wornBy != gActor)
<a name="510"></a>            return true;
<a name="511"></a>        
<a name="512"></a>        /* 
<a name="513"></a>         *   If we're allowed to attempt an implicit action, try taking off obj
<a name="514"></a>         *   implicitly and see if we succeed.
<a name="515"></a>         */        
<a name="516"></a>        if(allowImplicit) 
<a name="517"></a>        {
<a name="518"></a>            /* 
<a name="519"></a>             *   Try taking off obj implicitly and note if we were allowed to
<a name="520"></a>             *   make the attempt.
<a name="521"></a>             */
<a name="522"></a>            local tried = tryImplicitAction(Doff, obj);
<a name="523"></a>            
<a name="524"></a>            /*  
<a name="525"></a>             *   If obj is now not being worn return true to signal that this
<a name="526"></a>             *   precondition has now been met.
<a name="527"></a>             */
<a name="528"></a>            if(obj.wornBy == nil)
<a name="529"></a>                return true;
<a name="530"></a>            
<a name="531"></a>            /* 
<a name="532"></a>             *   Otherwise, if we tried but failed to take obj off, return nil
<a name="533"></a>             *   to signal that this precondition can't be met (so the main
<a name="534"></a>             *   action cannot proceed). The attempt to take off obj will have
<a name="535"></a>             *   explained why it failed, so there's no need for any further
<a name="536"></a>             *   explanation here.
<a name="537"></a>             */
<a name="538"></a>            if(tried)
<a name="539"></a>                return nil;
<a name="540"></a>            
<a name="541"></a>        }
<a name="542"></a>        
<a name="543"></a>        /* 
<a name="544"></a>         *   If we reach here obj is still being worn and we weren't allowed to
<a name="545"></a>         *   try to take it off; display a message explaining the problem.
<a name="546"></a>         */
<a name="547"></a>        gMessageParams(obj);
<a name="548"></a>        DMsg(cannot do that while wearing, '{I} {can\'t} do that while
<a name="549"></a>            {he actor}{\'m} wearing {the obj}. ');
<a name="550"></a>        
<a name="551"></a>        /* Then return nil to indicate that the precondition hasn't been met. */
<a name="552"></a>        return nil;        
<a name="553"></a>    }
<a name="554"></a>    
<a name="555"></a>;
<a name="556"></a>
<a name="557"></a>/* A PreCondition to check that an object is visible. */
<a name="558"></a>objVisible: PreCondition
<a name="559"></a>    verifyPreCondition(obj)
<a name="560"></a>    {
<a name="561"></a>        /* If it's too dark to see then we can't examine the object */        
<a name="562"></a>        if(!(gActor.outermostVisibleParent().isIlluminated || obj.visibleInDark))
<a name="563"></a>            inaccessible(obj.tooDarkToSeeMsg);
<a name="564"></a>        
<a name="565"></a>        /* If the actor can't see the obj then we can't examine the object. */
<a name="566"></a>        if(!Q.canSee(gActor, obj))
<a name="567"></a>            inaccessible(BMsg(cannot see obj, '{I} {can\'t} see {1}. ',
<a name="568"></a>                         obj.theName));
<a name="569"></a>    }   
<a name="570"></a>;
<a name="571"></a>
<a name="572"></a>/* A PreCondition to check that an object is audible. */
<a name="573"></a>objAudible: PreCondition
<a name="574"></a>    verifyPreCondition(obj)
<a name="575"></a>    {
<a name="576"></a>       /* 
<a name="577"></a>        *   Construct a list of objects that are blocking the sound path between
<a name="578"></a>        *   the actor and obj.
<a name="579"></a>        */
<a name="580"></a>        local lst = Q.soundBlocker(gActor, obj);
<a name="581"></a>        
<a name="582"></a>        /* 
<a name="583"></a>         *   If the actor cannot hear obj and there's at least one object
<a name="584"></a>         *   blocking the sound path between them, construct an appropriate
<a name="585"></a>         *   error message and block the action.
<a name="586"></a>         */      
<a name="587"></a>        if(!Q.canHear(gActor, obj) &amp;&amp; lst.length &gt; 0)
<a name="588"></a>        {            
<a name="589"></a>            local errMsg;
<a name="590"></a>            gMessageParams(obj);
<a name="591"></a>         
<a name="592"></a>            /* 
<a name="593"></a>             *   If the blocking object is a Room, then obj is in a remote
<a name="594"></a>             *   location, so the reason the actor can't hear it is that it's
<a name="595"></a>             *   too far away.
<a name="596"></a>             */
<a name="597"></a>            if(lst[1].ofKind(Room))
<a name="598"></a>                errMsg = BMsg(too far away to hear obj, '{The subj obj} {is} too
<a name="599"></a>                    far away to hear. ');
<a name="600"></a>            
<a name="601"></a>            /* 
<a name="602"></a>             *   Otherwise the reason the actor can't hear obj is that the first
<a name="603"></a>             *   blocking object is in the way.
<a name="604"></a>             */
<a name="605"></a>            else           
<a name="606"></a>                errMsg = BMsg(cannot hear, '{I} {can\'t} hear {1} 
<a name="607"></a>                through {2}. ', obj.theName, lst[1].theName);
<a name="608"></a>                
<a name="609"></a>            /* Declare obj to be inaccessible to hearing. */
<a name="610"></a>            inaccessible(errMsg);
<a name="611"></a>        }        
<a name="612"></a>    }
<a name="613"></a>;
<a name="614"></a>
<a name="615"></a>/* A PreCondition to check that an object is smellable. */
<a name="616"></a>objSmellable: PreCondition
<a name="617"></a>    verifyPreCondition(obj)
<a name="618"></a>    {
<a name="619"></a>       /* 
<a name="620"></a>        *   Construct a list of objects that are blocking the scent path between
<a name="621"></a>        *   the actor and obj.
<a name="622"></a>        */
<a name="623"></a>        local lst = Q.scentBlocker(gActor, obj);
<a name="624"></a>        
<a name="625"></a>        /* 
<a name="626"></a>         *   If the actor cannot hear obj and there's at least one object
<a name="627"></a>         *   blocking the sound path between them, construct an appropriate
<a name="628"></a>         *   error message and block the action.
<a name="629"></a>         */      
<a name="630"></a>        if(!Q.canSmell(gActor, obj) &amp;&amp; lst.length &gt; 0)
<a name="631"></a>        {            
<a name="632"></a>            local errMsg;
<a name="633"></a>            gMessageParams(obj);
<a name="634"></a>         
<a name="635"></a>            /* 
<a name="636"></a>             *   If the blocking object is a Room, then obj is in a remote
<a name="637"></a>             *   location, so the reason the actor can't smell it is that it's
<a name="638"></a>             *   too far away.
<a name="639"></a>             */
<a name="640"></a>            if(lst[1].ofKind(Room))
<a name="641"></a>                errMsg = BMsg(too far away to smell obj, '{The subj obj} {is}
<a name="642"></a>                    too far away to smell. ');
<a name="643"></a>            
<a name="644"></a>            /* 
<a name="645"></a>             *   Otherwise the reason the actor can't smell obj is that the first
<a name="646"></a>             *   blocking object is in the way.
<a name="647"></a>             */
<a name="648"></a>            else           
<a name="649"></a>                errMsg = BMsg(cannot smell through, '{I} {can\'t} smell {1} 
<a name="650"></a>                through {2}. ', obj.theName, lst[1].theName);
<a name="651"></a>                
<a name="652"></a>            /* Declare obj to be inaccessible to hearing. */
<a name="653"></a>            inaccessible(errMsg);
<a name="654"></a>        }        
<a name="655"></a>    }
<a name="656"></a>;
<a name="657"></a>
<a name="658"></a>/* 
<a name="659"></a> *   A PreCondition to check that an object can be touched (which is likely to
<a name="660"></a> *   be needed for any action that manipulated the object). This Precondition
<a name="661"></a> *   farms out as much of the detailed checking as possible to the Query object. 
<a name="662"></a> */
<a name="663"></a>touchObj: PreCondition    
<a name="664"></a>    verifyPreCondition(obj)
<a name="665"></a>    {
<a name="666"></a>        /* 
<a name="667"></a>         *   Store any issues that the Query object finds with reaching obj from
<a name="668"></a>         *   gActor
<a name="669"></a>         */
<a name="670"></a>        local reachIssues = Q.reachProblemVerify(gActor, obj);
<a name="671"></a>        
<a name="672"></a>        /*  Run the verify method of any issues we found */
<a name="673"></a>        foreach(local issue in reachIssues)        
<a name="674"></a>            issue.verify();        
<a name="675"></a>    }
<a name="676"></a>    
<a name="677"></a>    
<a name="678"></a>    
<a name="679"></a>    checkPreCondition(obj, allowImplicit)
<a name="680"></a>    {
<a name="681"></a>        /* 
<a name="682"></a>         *   Obtain any issues the Query object finds with reaching obj from
<a name="683"></a>         *   gActor at the check stage.
<a name="684"></a>         */
<a name="685"></a>        
<a name="686"></a>        local reachIssues = Q.reachProblemCheck(gActor, obj);
<a name="687"></a>        
<a name="688"></a>        /* 
<a name="689"></a>         *   Go through each stored issue in turn running its check method; if
<a name="690"></a>         *   any of the check methods return nil, exit and return nil.
<a name="691"></a>         */        
<a name="692"></a>        foreach(local issue in reachIssues)
<a name="693"></a>        {
<a name="694"></a>            if(issue.check(allowImplicit) == nil)
<a name="695"></a>                return nil;
<a name="696"></a>        }
<a name="697"></a>               
<a name="698"></a>        /* 
<a name="699"></a>         *   If we reach here we've passed all the checks, so return true to
<a name="700"></a>         *   indicate success.
<a name="701"></a>         */
<a name="702"></a>        return true;
<a name="703"></a>    }
<a name="704"></a>    
<a name="705"></a>    
<a name="706"></a>    preCondOrder = 80    
<a name="707"></a>;
<a name="708"></a>
<a name="709"></a>/* Declare attachedTo as a property since the attachables module is optional. */
<a name="710"></a>property attachedTo;
<a name="711"></a>property attachedToList;
<a name="712"></a>
<a name="713"></a>
<a name="714"></a>/* A PreCondition to check that an object isn't attached to anything. */
<a name="715"></a>objDetached: PreCondition
<a name="716"></a>    verifyPreCondition(obj)
<a name="717"></a>    {
<a name="718"></a>        /* 
<a name="719"></a>         *   An object that is attached may be a slightly less logical choice
<a name="720"></a>         *   for this action.
<a name="721"></a>         */
<a name="722"></a>        if(obj.attachedTo != nil)
<a name="723"></a>            logicalRank(90);
<a name="724"></a>    }
<a name="725"></a> 
<a name="726"></a>    checkPreCondition(obj, allowImplicit)
<a name="727"></a>    {
<a name="728"></a>        /* If the object isn't attached, we're done */
<a name="729"></a>        if(obj.attachedToList.length == 0)
<a name="730"></a>            return true;
<a name="731"></a>        
<a name="732"></a>         /* 
<a name="733"></a>          *   If we're allowed to attempt an implicit action, try detaching obj
<a name="734"></a>          *   implicitly and see if we succeed.
<a name="735"></a>          */
<a name="736"></a>        if(allowImplicit)            
<a name="737"></a>        {
<a name="738"></a>            /* 
<a name="739"></a>             *   Try detaching obj implicitly and note if we were allowed to
<a name="740"></a>             *   make the attempt.
<a name="741"></a>             */
<a name="742"></a>            local tried = nil;
<a name="743"></a>                
<a name="744"></a>            foreach(local cur in obj.attachedToList)
<a name="745"></a>                tried = tryImplicitAction(DetachFrom, obj, cur);
<a name="746"></a>            {
<a name="747"></a>                
<a name="748"></a>                /*  
<a name="749"></a>                 *   If obj is now not attached to anything return true to signal that this
<a name="750"></a>                 *   precondition has now been met.
<a name="751"></a>                 */
<a name="752"></a>                if(obj.attachedToList.length == 0)
<a name="753"></a>                    return true;
<a name="754"></a>                
<a name="755"></a>                /* 
<a name="756"></a>                 *   Otherwise, if we tried but failed to detach obj, return nil to signal that this
<a name="757"></a>                 *   precondition can't be met (so the main action cannot proceed). The attempt to
<a name="758"></a>                 *   detach obj will have explained why it failed, so there's no need for any
<a name="759"></a>                 *   further explanation here.
<a name="760"></a>                 */
<a name="761"></a>                if(tried)                    
<a name="762"></a>                    return nil;
<a name="763"></a>            }
<a name="764"></a>        }
<a name="765"></a>        
<a name="766"></a>        /* 
<a name="767"></a>         *   If we reach here obj is still attached to something and we weren't
<a name="768"></a>         *   allowed to try to detach it; display a message explaining the
<a name="769"></a>         *   problem.
<a name="770"></a>         */
<a name="771"></a>        local att = obj.attachedTo;
<a name="772"></a>        gMessageParams(obj, att);
<a name="773"></a>        
<a name="774"></a>        DMsg(cannot do that while attached, '{I} {can\'t} do that while {the subj
<a name="775"></a>            obj} is attached to {the att}. ');
<a name="776"></a>        
<a name="777"></a>        /* Then return nil to indicate that the precondition hasn't been met. */
<a name="778"></a>        return nil;  
<a name="779"></a>    }
<a name="780"></a>;
<a name="781"></a>
<a name="782"></a>
<a name="783"></a>/* ------------------------------------------------------------------------ */
<a name="784"></a>/*
<a name="785"></a> *   A pre-condition that applies to a specific, pre-determined object,
<a name="786"></a> *   rather than the direct/indirect object of the command.
<a name="787"></a> */
<a name="788"></a>class ObjectPreCondition: PreCondition
<a name="789"></a>    construct(obj, cond)
<a name="790"></a>    {
<a name="791"></a>        /* 
<a name="792"></a>         *   remember the specific object I act upon, and the underlying
<a name="793"></a>         *   precondition to apply to that object 
<a name="794"></a>         */
<a name="795"></a>        obj_ = obj;
<a name="796"></a>        cond_ = cond;
<a name="797"></a>    }
<a name="798"></a>
<a name="799"></a>    /* route our check to the pre-condition using our specific object */
<a name="800"></a>    checkPreCondition(obj, allowImplicit)
<a name="801"></a>    {
<a name="802"></a>        /* check the precondition */
<a name="803"></a>        return cond_.checkPreCondition(obj_, allowImplicit);
<a name="804"></a>    }
<a name="805"></a>
<a name="806"></a>    /* route our verification check to the pre-condition */
<a name="807"></a>    verifyPreCondition(obj)
<a name="808"></a>    {
<a name="809"></a>        cond_.verifyPreCondition(obj_);
<a name="810"></a>    }
<a name="811"></a>
<a name="812"></a>    /* use the same order as our underlying condition */
<a name="813"></a>    preCondOrder = (cond_.preCondOrder)
<a name="814"></a>
<a name="815"></a>    /* the object we check with the condition */
<a name="816"></a>    obj_ = nil
<a name="817"></a>
<a name="818"></a>    /* the pre-condition we check */
<a name="819"></a>    cond_ = nil
<a name="820"></a>;
<a name="821"></a>
<a name="822"></a>/* -------------------------------------------------------------------------- */
<a name="823"></a>
<a name="824"></a>actorInStagingLocation: PreCondition
<a name="825"></a>    checkPreCondition(obj, allowImplicit)
<a name="826"></a>    {
<a name="827"></a>        local loc = gActor.location;
<a name="828"></a>        local stagingLoc = obj.stagingLocation;
<a name="829"></a>        local action;
<a name="830"></a>        
<a name="831"></a>        /* If the actor's location is the staging location then we're done. */
<a name="832"></a>        if(loc == stagingLoc)
<a name="833"></a>            return true;
<a name="834"></a>        
<a name="835"></a>        if(stagingLoc == nil)
<a name="836"></a>        {
<a name="837"></a>            gMessageParams(obj);
<a name="838"></a>            DMsg(no staging loc, '{The subj obj} {can\'t} be reached. ');
<a name="839"></a>            return nil;
<a name="840"></a>        }
<a name="841"></a>        
<a name="842"></a>        if(allowImplicit)
<a name="843"></a>        {
<a name="844"></a>            local tried = nil;
<a name="845"></a>            
<a name="846"></a>            while(!stagingLoc.isOrIsIn(loc))
<a name="847"></a>            {
<a name="848"></a>                action = loc.contType == In ? GetOutOf : GetOff;
<a name="849"></a>                tried = tryImplicitAction(action, loc);
<a name="850"></a>                if(gActor.location == loc)
<a name="851"></a>                    break;
<a name="852"></a>                
<a name="853"></a>                loc = gActor.location;
<a name="854"></a>            }
<a name="855"></a>                                  
<a name="856"></a>            if(stagingLoc == loc)
<a name="857"></a>                return true;
<a name="858"></a>            
<a name="859"></a>            local path = [];
<a name="860"></a>            local step = stagingLoc;
<a name="861"></a>            
<a name="862"></a>            while(step &amp;&amp; step != loc)
<a name="863"></a>            {
<a name="864"></a>                path = [step] + path;
<a name="865"></a>                step = step.stagingLocation;
<a name="866"></a>            }
<a name="867"></a>            
<a name="868"></a>            foreach(step in path)
<a name="869"></a>            {
<a name="870"></a>                action = step.contType == In ? Enter : Board;
<a name="871"></a>                tried = tryImplicitAction(action, step);                
<a name="872"></a>                if(gActor.location != step)
<a name="873"></a>                    break;
<a name="874"></a>            }
<a name="875"></a>            
<a name="876"></a>            if(stagingLoc == gActor.location)
<a name="877"></a>                return true;
<a name="878"></a>            
<a name="879"></a>            if(tried)
<a name="880"></a>                return nil;
<a name="881"></a>            
<a name="882"></a>            
<a name="883"></a>        }
<a name="884"></a>        
<a name="885"></a>        gMessageParams(stagingLoc);
<a name="886"></a>        
<a name="887"></a>        DMsg(not in staging location, '{I} need{s/ed} to be &lt;&lt;if
<a name="888"></a>              stagingLoc.ofKind(Room)&gt;&gt; directly &lt;&lt;end&gt;&gt;
<a name="889"></a>            {in stagingloc} to do that. ');
<a name="890"></a>    
<a name="891"></a>         /* Then return nil to indicate that the precondition hasn't been met. */
<a name="892"></a>        return nil;  
<a name="893"></a>        
<a name="894"></a>    }    
<a name="895"></a>;
<a name="896"></a>
<a name="897"></a>/* 
<a name="898"></a> *   A PreCondition to make sure that the actor is out of any nested rooms
<a name="899"></a> *   before the action takes place.
<a name="900"></a> */
<a name="901"></a>actorOutOfNested: PreCondition
<a name="902"></a>    checkPreCondition(obj, allowImplicit)
<a name="903"></a>    {
<a name="904"></a>        local loc = gActor.location;
<a name="905"></a>        local action;
<a name="906"></a>        
<a name="907"></a>        /* 
<a name="908"></a>         *   If the Actor's location is a room, then there's nothing to be done.
<a name="909"></a>         */
<a name="910"></a>        if(loc.ofKind(Room))
<a name="911"></a>            return true;
<a name="912"></a>        
<a name="913"></a>        if(allowImplicit)
<a name="914"></a>        {
<a name="915"></a>            local tried = nil;
<a name="916"></a>            while(!loc.ofKind(Room))
<a name="917"></a>            {
<a name="918"></a>                action = loc.contType == In ? GetOutOf : GetOff;
<a name="919"></a>                tried = tryImplicitAction(action, loc);
<a name="920"></a>                if(gActor.location == loc)
<a name="921"></a>                    break;
<a name="922"></a>                
<a name="923"></a>                loc = gActor.location;                
<a name="924"></a>            }
<a name="925"></a>            
<a name="926"></a>            /* If the actor is now in a Room, we've succeeded. */
<a name="927"></a>            if(loc.ofKind(Room))
<a name="928"></a>                return true;
<a name="929"></a>            
<a name="930"></a>            /* 
<a name="931"></a>             *   Otherwise, if we tried but failed to get out into the Room,
<a name="932"></a>             *   return nil to tell our caller we failed.
<a name="933"></a>             */
<a name="934"></a>            if(tried)
<a name="935"></a>                return nil;            
<a name="936"></a>        }
<a name="937"></a>        
<a name="938"></a>        gMessageParams(loc);
<a name="939"></a>        DMsg(still in nested, '{I} {can\'t} do that while {he actor} {is} 
<a name="940"></a>            {in loc}. ');
<a name="941"></a>        return nil;
<a name="942"></a>    }
<a name="943"></a>    
<a name="944"></a>;
<a name="945"></a>
<a name="946"></a>/* 
<a name="947"></a> *   A precondition to ensure that an actor is out of any enclosed nested rooms before getting
<a name="948"></a> *   off/out of the enclosing nested room.
<a name="949"></a> */
<a name="950"></a>actorOutOfSubNested: PreCondition
<a name="951"></a>    checkPreCondition(obj, allowImplicit)
<a name="952"></a>    {
<a name="953"></a>        /* First stache the actor's current location. */
<a name="954"></a>        local loc = gActor.location;
<a name="955"></a>        
<a name="956"></a>        /* 
<a name="957"></a>         *   If we're allowed to perform an implicit action, then try moving the actor out of any
<a name="958"></a>         *   nested rooms that are nested within the nested room we're trying to leave.
<a name="959"></a>         */
<a name="960"></a>        if(allowImplicit)
<a name="961"></a>        {
<a name="962"></a>            while(loc != obj)
<a name="963"></a>            {
<a name="964"></a>                /* The action we need to exit the nested room we're in depends on its contType. */
<a name="965"></a>                local getOutAction = (loc.contType == On ? GetOff : GetOutOf);
<a name="966"></a>                
<a name="967"></a>                /* 
<a name="968"></a>                 *   Try performing the action we need to exit our immediate container. If we fail,
<a name="969"></a>                 *   return nil to tell our caller the main action can't proceed.
<a name="970"></a>                 */
<a name="971"></a>                if(!tryImplicitAction(getOutAction, loc))
<a name="972"></a>                    return nil;
<a name="973"></a>                
<a name="974"></a>                /* If the actor hasn't moved, break out of the loop. */
<a name="975"></a>                if(gActor.location == loc)
<a name="976"></a>                    break;
<a name="977"></a>                
<a name="978"></a>                /* Note the actor's new location. */
<a name="979"></a>                loc = gActor.location;                
<a name="980"></a>            }          
<a name="981"></a>        }
<a name="982"></a>        
<a name="983"></a>        /* 
<a name="984"></a>         *   If the actor's location is the object the actor is trying to exit, then we're good to
<a name="985"></a>         *   go, so return true to tell our caller the main action can proceed.
<a name="986"></a>         */
<a name="987"></a>        if(loc == obj)
<a name="988"></a>            return true;  
<a name="989"></a>        
<a name="990"></a>        /* 
<a name="991"></a>         *   Otherwise display our failure message and return nil to tell our caller the main action
<a name="992"></a>         *   cannot go ahead.
<a name="993"></a>         */
<a name="994"></a>        DMsg(not out of subnested, '{I} need{s/ed} to be {1} before {i} {can} get {2}. ',
<a name="995"></a>            loc.objOutOfName, obj.objOutOfName);
<a name="996"></a>        
<a name="997"></a>        return nil;
<a name="998"></a>    }
<a name="999"></a>;
<a name="1000"></a>        
<a name="1001"></a>
<a name="1002"></a>
<a name="1003"></a>
<a name="1004"></a>
<a name="1005"></a>/*  
<a name="1006"></a> *   PreCondition that calls before travel notifications (such as beforeTravel() on every object in
<a name="1007"></a> *   scope) so these can rule out travel/push travel before it's attempted.
<a name="1008"></a> */
<a name="1009"></a>travelPermitted: PreCondition
<a name="1010"></a>    checkPreCondition(obj, allowImplicit) 
<a name="1011"></a>    { 
<a name="1012"></a>      
<a name="1013"></a>        obj.beforeTravelNotifications(gActor);
<a name="1014"></a>        
<a name="1015"></a>        if(gAction.isPushTravelAction)        
<a name="1016"></a>            obj.beforeTravelNotifications(gDobj);
<a name="1017"></a>        
<a name="1018"></a>        return true;
<a name="1019"></a>    }
<a name="1020"></a>    
<a name="1021"></a>    preCondOrder = 50
<a name="1022"></a>;
<a name="1023"></a>
<a name="1024"></a>
<a name="1025"></a>
<a name="1026"></a>/* 
<a name="1027"></a> *   A PreCondition to check whether an object counts aa currently in the actor's inventory (for the
<a name="1028"></a> *   purposes of being dropped). 
<a name="1029"></a> */
<a name="1030"></a>objCarried: PreCondition
<a name="1031"></a>    
<a name="1032"></a>    verifyPreCondition(obj)
<a name="1033"></a>    {
<a name="1034"></a>        /* 
<a name="1035"></a>         *   If the object is fixed in place it can't be picked up, so there's
<a name="1036"></a>         *   no point in trying. BUT we also have to check for the case that the
<a name="1037"></a>         *   object is directly in the player (perhaps because a body part).
<a name="1038"></a>         */        
<a name="1039"></a>        if(obj.isFixed &amp;&amp; !obj.isDirectlyIn(gActor))
<a name="1040"></a>            illogical(obj.cannotTakeMsg);
<a name="1041"></a>        
<a name="1042"></a>        
<a name="1043"></a>        /* 
<a name="1044"></a>         *   If the actor isn't carrying the object it's not in the actor's inventory,
<a name="1045"></a>         */        
<a name="1046"></a>        else if(!obj.isIn(gActor))
<a name="1047"></a>            illogicalNow(obj.notHoldingMsg);
<a name="1048"></a>        
<a name="1049"></a>        /*
<a name="1050"></a>         *   If the object isn't directly in the actor and the object's location doesn't allow
<a name="1051"></a>         *   objects to be dropoped from it, then it doesn't count as being in the actor's inventory
<a name="1052"></a>         *   for the purpose of dropping.
<a name="1053"></a>         */
<a name="1054"></a>        else if(obj.location != gActor &amp;&amp; !obj.location.canDropContents)
<a name="1055"></a>            illogicalNow(obj.notHoldingMsg);
<a name="1056"></a>        
<a name="1057"></a>        
<a name="1058"></a>        
<a name="1059"></a>        
<a name="1060"></a>        else
<a name="1061"></a>            logical;
<a name="1062"></a>        
<a name="1063"></a>        
<a name="1064"></a>        
<a name="1065"></a>    }
<a name="1066"></a>    
<a name="1067"></a>    checkPreCondition(obj, allowImplicit) 
<a name="1068"></a>    { 
<a name="1069"></a>        /* 
<a name="1070"></a>         *   if the object is already held, we're already done.
<a name="1071"></a>         */
<a name="1072"></a>        if (obj == nil || obj.isDirectlyIn(gActor))
<a name="1073"></a>            return true;
<a name="1074"></a>        
<a name="1075"></a>        
<a name="1076"></a>        /* 
<a name="1077"></a>         *   If we're allowed to attempt an implicit action, and obj's location allows it, try
<a name="1078"></a>         *   taking obj implicitly and see if we succeed.
<a name="1079"></a>         */
<a name="1080"></a>        if(allowImplicit &amp;&amp; obj.location.canDropContents) 
<a name="1081"></a>        {    
<a name="1082"></a>            /* 
<a name="1083"></a>             *   Try taking obj implicitly and note if we were allowed to make
<a name="1084"></a>             *   the attempt.
<a name="1085"></a>             */
<a name="1086"></a>            local tried = tryImplicitAction(TakeFrom, obj, obj.location);
<a name="1087"></a>            
<a name="1088"></a>            /*  
<a name="1089"></a>             *   If obj is now held by the actor return true to signal that this
<a name="1090"></a>             *   precondition has now been met.
<a name="1091"></a>             */
<a name="1092"></a>            if(obj.isDirectlyIn(gActor))
<a name="1093"></a>              return true;   
<a name="1094"></a>            
<a name="1095"></a>            /* 
<a name="1096"></a>             *   Otherwise, if we tried but failed to take obj, return nil to
<a name="1097"></a>             *   signal that this precondition can't be met (so the main action
<a name="1098"></a>             *   cannot proceed). The attempt to take obj will have explained
<a name="1099"></a>             *   why it failed, so there's no need for any further explanation
<a name="1100"></a>             *   here.
<a name="1101"></a>             */
<a name="1102"></a>            if(tried)
<a name="1103"></a>                return nil;            
<a name="1104"></a>        }
<a name="1105"></a>        
<a name="1106"></a>        /* 
<a name="1107"></a>         *   If we reach here obj isn't being held by the actor and we weren't
<a name="1108"></a>         *   allowed to try to take it; display a message explaining the
<a name="1109"></a>         *   problem.
<a name="1110"></a>         */
<a name="1111"></a>        gMessageParams(obj);
<a name="1112"></a>        DMsg(not held, '{I} {am} not holding {the obj}. ');
<a name="1113"></a>        
<a name="1114"></a>        /* Then return nil to indicate that the precondition hasn't been met. */
<a name="1115"></a>        return nil;        
<a name="1116"></a>    }
<a name="1117"></a>;
<a name="1118"></a>
<a name="1119"></a>/* A Precondition to check whether the actor can talk to obj. */
<a name="1120"></a>canTalkToObj: PreCondition    
<a name="1121"></a>    verifyPreCondition(obj)    
<a name="1122"></a>    {
<a name="1123"></a>        if(!obj.canTalkToMe)
<a name="1124"></a>            illogical(obj.cannotTalkToMsg); 
<a name="1125"></a>        
<a name="1126"></a>        else if(!gActor.canTalkTo(obj))
<a name="1127"></a>        {
<a name="1128"></a>            local errMsg;
<a name="1129"></a>            gMessageParams(obj);
<a name="1130"></a>            
<a name="1131"></a>            if(!obj.isIn(gActorRoom))
<a name="1132"></a>                errMsg = BMsg(too far away to talk, '{The subj obj} {is} too far away to talk to. ');
<a name="1133"></a>            else
<a name="1134"></a>                errMsg = BMsg(cannot talk to obj, '{I} {can\'t} talk to {the obj} just {now}. ');
<a name="1135"></a>            
<a name="1136"></a>            inaccessible(errMsg);
<a name="1137"></a>        }        
<a name="1138"></a>    }
<a name="1139"></a>;
<a name="1140"></a>    
<a name="1141"></a>    
</pre>
<div class=ftr>Adv3Lite Library Reference Manual<br>Generated on 25/01/2025 from adv3Lite version 2.1.1.14</div>
</body>
</html>
