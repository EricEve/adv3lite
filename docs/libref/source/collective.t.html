<html><head><link rel=stylesheet type="text/css" href="../libref.css"><title>collective.t</title></head><body>
<table class=ban><tr><td><h1>collective.t</h1><td align=right><a href="../file/collective.t.html">documentation</a></table><pre>
<a name="1"></a>#charset "us-ascii"
<a name="2"></a>#include "advlite.h"
<a name="3"></a>
<a name="4"></a>
<a name="5"></a>/* 
<a name="6"></a> * COLLECTIVE EXTENSION
<a name="7"></a> * 
<a name="8"></a> */
<a name="9"></a>
<a name="10"></a>/* ------------------------------------------------------------------------ */
<a name="11"></a>/*
<a name="12"></a> *   Collective - this is an object that can be used to refer to a group of
<a name="13"></a> *   other (usually equivalent) objects collectively.  In most cases, this
<a name="14"></a> *   object will be a separate game object that contains or can contain the
<a name="15"></a> *   individuals: a bag of marbles can be a collective for the marbles, or
<a name="16"></a> *   a book of matches can be a collective for the matchsticks.
<a name="17"></a> *   
<a name="18"></a> *   A collective object is usually given the same plural vocabulary as its
<a name="19"></a> *   individuals.  When we use that plural vocabulary, we will filter for
<a name="20"></a> *   or against the collective, as determined by the noun phrase
<a name="21"></a> *   production, when the player uses the collective term. 
<a name="22"></a> *   [COLLECTIVE EXTENSION]
<a name="23"></a> */
<a name="24"></a>class Collective: Thing
<a name="25"></a>    /* 
<a name="26"></a>     *   A list of one or more tokens, any of which might be used to refer to me
<a name="27"></a>     *   as a plural object. E.g. if the Collective is a bunch of grapes,
<a name="28"></a>     *   pluralTokes might be ['grapes']. This is used so that I'm chosen in
<a name="29"></a>     *   preference to individual objects (e.g. grapes) when the player's
<a name="30"></a>     *   command includes one of these plural tokens, but an individual object
<a name="31"></a>     *   (e.g. a grape) is preferred otherwise.
<a name="32"></a>     *
<a name="33"></a>     *   By default (if this is left as nil by the user) the library populates
<a name="34"></a>     *   this list from our name property, which in the majority of cases should
<a name="35"></a>     *   achieve what's needed. This property only needs to be user-defined in
<a name="36"></a>     *   cases where using the name won't work, even in conjunction with the
<a name="37"></a>     *   extraToks property.
<a name="38"></a>     */
<a name="39"></a>    collectiveToks = nil
<a name="40"></a>   
<a name="41"></a>    /*   
<a name="42"></a>     *   A list of additional tokens added to our collectiveToks at preInit.
<a name="43"></a>     *   This might be needed, for example, if a Collective called 'stack of
<a name="44"></a>     *   cans' also answers to 'pile of tins' (i.e., if it has 'pile' and 'tins'
<a name="45"></a>     *   defined as additional nouns it its vocab property), in which case we'd
<a name="46"></a>     *   need to defined these tokens ['pile', 'tins'] here.
<a name="47"></a>     */
<a name="48"></a>    extraToks = nil
<a name="49"></a>    
<a name="50"></a>    
<a name="51"></a>    /* Did the player's command match any of our collectiveToks ? [COLLECTIVE EXTENSION] */
<a name="52"></a>    collectiveDobjMatch = nil
<a name="53"></a>    
<a name="54"></a>	/* Did the player's command match any of our collectiveToks ? [COLLECTIVE EXTENSION] */
<a name="55"></a>    collectiveIobjMatch = nil
<a name="56"></a>    
<a name="57"></a>    /* The number of dispensible items being requested. */
<a name="58"></a>    numberWanted = 0
<a name="59"></a>    
<a name="60"></a>	/* For the COLLECTIVE EXTENSION  decide whether to select the collective object (ourselves) or an individual item. */
<a name="61"></a>    filterResolveList(np, cmd, mode)
<a name="62"></a>    {
<a name="63"></a>        /* Carry out any inherited handling */
<a name="64"></a>        inherited(np, cmd, mode);
<a name="65"></a>        
<a name="66"></a>        local num = np.quantifier == nil ? 1 : np.quantifier;
<a name="67"></a>        
<a name="68"></a>        local collectiveMatch = np.tokens.overlapsWith(collectiveToks) 
<a name="69"></a>            &amp;&amp; num == 1;
<a name="70"></a>        
<a name="71"></a>        /* 
<a name="72"></a>         *   Note whether we have a collective match in the particular role
<a name="73"></a>         *   being considered.
<a name="74"></a>         */
<a name="75"></a>        if(np.role == DirectObject)
<a name="76"></a>        {
<a name="77"></a>            collectiveDobjMatch = collectiveMatch;
<a name="78"></a>            
<a name="79"></a>            /* Note the numberWanted */
<a name="80"></a>            numberWanted = num;
<a name="81"></a>        }
<a name="82"></a>        if(np.role == IndirectObject)
<a name="83"></a>            collectiveIobjMatch = collectiveMatch;
<a name="84"></a>        
<a name="85"></a>        /* Go through each of the NPMatch objects in our list. */
<a name="86"></a>        foreach(local cur in np.matches)
<a name="87"></a>        {
<a name="88"></a>            /* 
<a name="89"></a>             *   Only worry about it if the NPMatch object's associated object
<a name="90"></a>             *   is one for which we're a collective. If it is, we need to make
<a name="91"></a>             *   a decision.
<a name="92"></a>             */
<a name="93"></a>            if(isCollectiveFor(cur.obj))
<a name="94"></a>            {
<a name="95"></a>                /*  
<a name="96"></a>                 *   If the player's command included one of our collectiveToks,
<a name="97"></a>                 *   then we're the preferred object to use, so remove cur from
<a name="98"></a>                 *   the list of possible matches.
<a name="99"></a>                 */
<a name="100"></a>                if(collectiveMatch || collectiveAction(np, cmd))
<a name="101"></a>                    np.matches -= cur;
<a name="102"></a>                
<a name="103"></a>                /*  Otherwise prefer the individual item. */
<a name="104"></a>                else
<a name="105"></a>                {                    
<a name="106"></a>                    /* So remove our NPMatch object from the list of matches. */
<a name="107"></a>                    np.matches = np.matches.subset({m: m.obj != self});
<a name="108"></a>                    
<a name="109"></a>                    /* 
<a name="110"></a>                     *   Then stop looping through the match list, since there's
<a name="111"></a>                     *   no more to do.
<a name="112"></a>                     */
<a name="113"></a>                    break;
<a name="114"></a>                }
<a name="115"></a>            }
<a name="116"></a>        }
<a name="117"></a>        
<a name="118"></a>    }
<a name="119"></a>    
<a name="120"></a>    /*
<a name="121"></a>     *   Determine if I'm a collective object for the given object.
<a name="122"></a>     *
<a name="123"></a>     *   In order to be a collective for some objects, an object must have
<a name="124"></a>     *   vocabulary for the plural name, and must return true from this method
<a name="125"></a>     *   for the collected objects.
<a name="126"></a>     *
<a name="127"></a>     *   The cmd parameter can be use to determine the current action
<a name="128"></a>     *   (cmd.action), in case we want to vary our decision according to the
<a name="129"></a>     *   action.
<a name="130"></a>     *
<a name="131"></a>     *   The np parameter can be used, inter alia, to determine the role
<a name="132"></a>     *   (np.role), e.g. as DirectObject or IndirectObject.
<a name="133"></a>	 *
<a name="134"></a>	 *   [COLLECTIVE EXTENSION]
<a name="135"></a>     */     
<a name="136"></a>    isCollectiveFor(obj) { return nil; }
<a name="137"></a>    
<a name="138"></a>    
<a name="139"></a>    collectiveAction(np, cmd) { return nil; }
<a name="140"></a>    
<a name="141"></a>    /* Overidden for COLLECTIVE EXTENSION */
<a name="142"></a>    preinitThing
<a name="143"></a>    {
<a name="144"></a>        inherited();
<a name="145"></a>        
<a name="146"></a>        /* 
<a name="147"></a>         *   If collectiveToks hasn't been defined, populate it with tokens
<a name="148"></a>         *   drawn from our name plus any user-defined extraToks.
<a name="149"></a>         */
<a name="150"></a>        if(collectiveToks == nil)
<a name="151"></a>            collectiveToks = name.toLower.split(' ') + valToList(extraToks);
<a name="152"></a>    }
<a name="153"></a>;
<a name="154"></a>
<a name="155"></a>/*   
<a name="156"></a> *   A DispensingCollective is a Collective that dispenses objects when the
<a name="157"></a> *   player takes from it; e.g. a bunch of grapes that dispenses grapes.
<a name="158"></a> *   [COLLECTIVE EXTENSION]
<a name="159"></a> */
<a name="160"></a>class DispensingCollective: Collective
<a name="161"></a>    
<a name="162"></a>    /*  
<a name="163"></a>     *   If definesd the class of object that is created and dispensed when an
<a name="164"></a>     *   actor takes from this DispensingCollective. [COLLECTIVE EXTENSION]
<a name="165"></a>     */
<a name="166"></a>    dispensedClass = nil
<a name="167"></a>    
<a name="168"></a>    /*   
<a name="169"></a>     *   Alternatively, a list of objects that are taken in turn when we take
<a name="170"></a>     *   from this DispensingCollective. [COLLECTIVE EXTENSION]
<a name="171"></a>     */
<a name="172"></a>    dispensedObjs = nil
<a name="173"></a>    
<a name="174"></a>    /*   The number of objects we have dispensed so far. [COLLECTIVE EXTENSION] */
<a name="175"></a>    dispensedCount = 0
<a name="176"></a>    
<a name="177"></a>    /*   
<a name="178"></a>     *   The total number of objects we can dispense. If this is nil, there is
<a name="179"></a>     *   no limit. [COLLECTIVE EXTENSION]
<a name="180"></a>     */
<a name="181"></a>    maxToDispense = nil
<a name="182"></a>    
<a name="183"></a>    /*   
<a name="184"></a>     *   The number of objects we have left to dispense. This is updated by the
<a name="185"></a>     *   canDispense method, and shouldn't be overridden by user code. It may,
<a name="186"></a>     *   however, for use code to consult this property, e.g. to vary our
<a name="187"></a>     *   description. [COLLECTIVE EXTENSION]
<a name="188"></a>     */         
<a name="189"></a>    numLeft = 0
<a name="190"></a>    
<a name="191"></a>    /*  
<a name="192"></a>     *   In principle a DispensingCollective can supply additional items on
<a name="193"></a>     *   demand. This property is used by the parser to prevent it from throwing
<a name="194"></a>     *   an error when the player asks for more of the items we dispense than
<a name="195"></a>     *   are currently in scope. [COLLECTIVE EXTENSION]
<a name="196"></a>     */
<a name="197"></a>    canSupply = true
<a name="198"></a>    
<a name="199"></a>    /*   Is it possible (or allowed) to dispense any more objects from us? [COLLECTIVE EXTENSION] */
<a name="200"></a>    canDispense()
<a name="201"></a>    {
<a name="202"></a>        /* 
<a name="203"></a>         *   If we have a list of dispensedObjs, we can continue to dispense
<a name="204"></a>         *   until we reach the end of the list.
<a name="205"></a>         */
<a name="206"></a>        if(dispensedObjs)
<a name="207"></a>        {
<a name="208"></a>            numLeft = valToList(dispensedObjs).length - dispensedCount;       
<a name="209"></a>        }
<a name="210"></a>        
<a name="211"></a>        /*  
<a name="212"></a>         *   Otherwise, if we have a dispensed class we can continue to dispense
<a name="213"></a>         *   either if there is no limit to the number we can dispense
<a name="214"></a>         *   (maxToDispense = nil) or until we have dispensed maxToDispense
<a name="215"></a>         *   objects.
<a name="216"></a>         */
<a name="217"></a>        if(dispensedClass)
<a name="218"></a>        {
<a name="219"></a>            /* 
<a name="220"></a>             *   There's no limit to the number we can dispense, so simply
<a name="221"></a>             *   return true.
<a name="222"></a>             */
<a name="223"></a>            if(maxToDispense == nil)
<a name="224"></a>                return true;
<a name="225"></a>            
<a name="226"></a>            numLeft = maxToDispense - dispensedCount;     
<a name="227"></a>        }
<a name="228"></a>        
<a name="229"></a>        /*  
<a name="230"></a>         *   We can dispense the number of objects asked provided that number
<a name="231"></a>         *   is no greater than the number we have left.
<a name="232"></a>         */
<a name="233"></a>        return numberWanted &lt;= numLeft;;           
<a name="234"></a>    }
<a name="235"></a>    
<a name="236"></a>    /*  Dispense an object from this DispensingCollective. [COLLECTIVE EXTENSION]*/
<a name="237"></a>    dispenseObj()
<a name="238"></a>    {
<a name="239"></a>        for(local i in 1..numberWanted)
<a name="240"></a>        {
<a name="241"></a>            /* Increase the count of the number of objects dispensed. */
<a name="242"></a>            dispensedCount++;
<a name="243"></a>            
<a name="244"></a>            /* If we have a list of dispensedObjs, select the next one. */
<a name="245"></a>            if(dispensedObjs)
<a name="246"></a>            {
<a name="247"></a>                obj = valToList(dispensedObjs)[dispensedCount];            
<a name="248"></a>            }
<a name="249"></a>            /*  Otherwise create a new object of our dispensedClass class. */
<a name="250"></a>            else
<a name="251"></a>                obj = dispensedClass.createInstance();
<a name="252"></a>            
<a name="253"></a>            
<a name="254"></a>            /* 
<a name="255"></a>             *   Check that the actor has room to hold obj, and stop the action
<a name="256"></a>             *   if not.
<a name="257"></a>             */
<a name="258"></a>            if(gOutStream.watchForOutput({: obj.checkRoomToHold() }))
<a name="259"></a>                exit;
<a name="260"></a>            
<a name="261"></a>            /*  Move the object into the actor's inventory. */
<a name="262"></a>            obj.actionMoveInto(gActor);           
<a name="263"></a>            
<a name="264"></a>            
<a name="265"></a>            /*  
<a name="266"></a>             *   If we have now dispensed all the objects we can, carry out any
<a name="267"></a>             *   appropriate adjustments
<a name="268"></a>             */
<a name="269"></a>            
<a name="270"></a>            if(dispensedCount == maxToDispense)
<a name="271"></a>                exhaustDispenser();
<a name="272"></a>        }
<a name="273"></a>        
<a name="274"></a>         /*  Say that we have dispensed the object. */
<a name="275"></a>        sayDispensed(obj);
<a name="276"></a>    }
<a name="277"></a>    
<a name="278"></a>    /* Display a message saying that the actor has taken an object from us. [COLLECTIVE EXTENSION]*/
<a name="279"></a>    sayDispensed(obj)
<a name="280"></a>    {               
<a name="281"></a>        local objDesc = numberWanted == 1 ? obj.aName :
<a name="282"></a>        spellNumber(numberWanted) + ' ' + obj.pluralNameFrom(obj.name);
<a name="283"></a>        
<a name="284"></a>        DMsg(say dispensed, '{I} {take} {1} from {2}. ', objDesc, dispenserName);
<a name="285"></a>        
<a name="286"></a>    }
<a name="287"></a>    
<a name="288"></a>    /* 
<a name="289"></a>     *   The name to use when reporting that something has been dispensed (i.e. taken) from us.
<a name="290"></a>     *   Normally this will simply be our theName but this can be overridden in game code, e.g. to
<a name="291"></a>     *   'one of the apple trees' when the dispenser is the apples on the tree.
<a name="292"></a>     */
<a name="293"></a>    dispenserName = theName
<a name="294"></a>    
<a name="295"></a>    /* 
<a name="296"></a>     *   The name to use when reportingt that we can't take any more items from us. Normally this
<a name="297"></a>     *   will be our dispenser name, but particularl where the DispensingCollective represents a
<a name="298"></a>     *   plural, such as trees in an orchard, we might want to use something different, e.g. 'any of
<a name="299"></a>     *   the apple trees'.
<a name="300"></a>     */
<a name="301"></a>    exhaustedName = dispenserName
<a name="302"></a>    
<a name="303"></a>    /*  
<a name="304"></a>     *   Game code can override this method on specific objects to carry out the
<a name="305"></a>     *   effects of dispensing the maximum number of objects we're going to
<a name="306"></a>     *   dispense, e.g. by changing the description, or replacing a bunch of
<a name="307"></a>     *   bananas by a single banana (when it's the last one left). We do nothing
<a name="308"></a>     *   here in the library, since what's needed will vary with the specifics
<a name="309"></a>     *   of the game. [COLLECTIVE EXTENSION]
<a name="310"></a>     */
<a name="311"></a>    exhaustDispenser()
<a name="312"></a>    {
<a name="313"></a>    }
<a name="314"></a>    
<a name="315"></a>    /* 
<a name="316"></a>     *   The TAKE action applied to a DispensingCollective might mean one of two
<a name="317"></a>     *   things: it might be an attempt to take the DispensingCollective (e.g.
<a name="318"></a>     *   the bunch of grapes) itself, or it may be an attempt to take a single
<a name="319"></a>     *   item (e.g. a single grape from the bunch). We assume it's the former if
<a name="320"></a>     *   what the player typed matches the plural vocab (e.g. 'grapes') and the
<a name="321"></a>     *   latter otherwise. [COLLECTIVE EXTENSION]
<a name="322"></a>     */
<a name="323"></a>    dobjFor(Take)
<a name="324"></a>    {
<a name="325"></a>        verify()
<a name="326"></a>        {
<a name="327"></a>            /* 
<a name="328"></a>             *   If it's an attempt to take us, rather than an item from us, use
<a name="329"></a>             *   the inherited handling.
<a name="330"></a>             */
<a name="331"></a>            if(collectiveDobjMatch)
<a name="332"></a>                inherited;
<a name="333"></a>        }
<a name="334"></a>        
<a name="335"></a>        check()
<a name="336"></a>        {
<a name="337"></a>            /* 
<a name="338"></a>             *   If it's an attempt to take us, rather than an item from us, use
<a name="339"></a>             *   the inherited handling.
<a name="340"></a>             */
<a name="341"></a>            if(collectiveDobjMatch)
<a name="342"></a>                inherited;
<a name="343"></a>            
<a name="344"></a>            /*  
<a name="345"></a>             *   Otherwise see if we're able to dispense another item and
<a name="346"></a>             *   complain if we can't.
<a name="347"></a>             */
<a name="348"></a>            else if(!canDispense)
<a name="349"></a>                sayCannotDispense();
<a name="350"></a>        }
<a name="351"></a>        
<a name="352"></a>        action()
<a name="353"></a>        {
<a name="354"></a>            /* 
<a name="355"></a>             *   If it's an attempt to take us, rather than an item from us, use
<a name="356"></a>             *   the inherited handling.
<a name="357"></a>             */
<a name="358"></a>            if(collectiveDobjMatch)
<a name="359"></a>                inherited;
<a name="360"></a>            
<a name="361"></a>            /*  
<a name="362"></a>             *   Otherwise, dispense an item (e.g. take a single grape from the
<a name="363"></a>             *   bunch of grapes.
<a name="364"></a>             */
<a name="365"></a>            else
<a name="366"></a>                dispenseObj();
<a name="367"></a>        }
<a name="368"></a>    }
<a name="369"></a>    
<a name="370"></a>    /*  
<a name="371"></a>     *   We need to be able to handle commands like TAKE GRAPE FROM BUNCH where
<a name="372"></a>     *   the DispensingCollective represents both objects in the command (since
<a name="373"></a>     *   until we actually take the grape - or whatever the dispensed object is
<a name="374"></a>     *   to be - it doesn't yet exist in scope to be the object of the command.)
<a name="375"></a>	 *   [COLLECTIVE EXTENSION]
<a name="376"></a>     */
<a name="377"></a>    iobjFor(TakeFrom)
<a name="378"></a>    {
<a name="379"></a>        verify()
<a name="380"></a>        {
<a name="381"></a>            if(dispensedObjs &amp;&amp; gTentativeDobj.overlapsWith(dispensedObjs))
<a name="382"></a>                logical;
<a name="383"></a>            
<a name="384"></a>            else if(dispensedClass &amp;&amp; gTentativeDobj.indexWhich({o:
<a name="385"></a>                o.ofKind(dispensedClass) } ))
<a name="386"></a>                logical;
<a name="387"></a>            
<a name="388"></a>            else if(gTentativeDobj.indexOf(self))
<a name="389"></a>                logicalRank(120);    
<a name="390"></a>            
<a name="391"></a>            else
<a name="392"></a>                illogical(cannotTakeFromHereMsg);                   
<a name="393"></a>        }
<a name="394"></a>    }
<a name="395"></a>    
<a name="396"></a>    /*	[COLLECTIVE EXTENSION] */
<a name="397"></a>    cannotTakeFromHereMsg = BMsg(cant take from dispenser, '{I} {can\'t} take {a
<a name="398"></a>        dobj} from {1}. ', exhaustedName)
<a name="399"></a>    
<a name="400"></a>    /*	[COLLECTIVE EXTENSION] */
<a name="401"></a>    dobjFor(TakeFrom)
<a name="402"></a>    {
<a name="403"></a>        verify()
<a name="404"></a>        {
<a name="405"></a>            if(gIobj == self)
<a name="406"></a>                logicalRank(120);
<a name="407"></a>            else
<a name="408"></a>                inherited;
<a name="409"></a>        }
<a name="410"></a>        
<a name="411"></a>        check() { checkDobjTake(); }
<a name="412"></a>        action() { actionDobjTake(); }
<a name="413"></a>    }
<a name="414"></a>    
<a name="415"></a>    /* [COLLECTIVE EXTENSION] */
<a name="416"></a>    sayCannotDispense()
<a name="417"></a>    {
<a name="418"></a>        if(numLeft &lt; 1)
<a name="419"></a>            say(cannotDispenseMsg);
<a name="420"></a>        else
<a name="421"></a>            say(notEnoughLeftMsg);
<a name="422"></a>    }
<a name="423"></a>    
<a name="424"></a>    /* The message to display when there's no more items to dispense from us. [COLLECTIVE EXTENSION]*/
<a name="425"></a>    cannotDispenseMsg = BMsg(cannot dispense, '{I} {can\'t} take any more from
<a name="426"></a>        {1}. ', exhaustedName )
<a name="427"></a>    
<a name="428"></a>    /* 
<a name="429"></a>     *   The message to display when the player has asked us for more items than
<a name="430"></a>     *   we have left. [COLLECTIVE EXTENSION]
<a name="431"></a>     */
<a name="432"></a>    notEnoughLeftMsg = BMsg(not that many left, 'There{plural} {aren\'t} that
<a name="433"></a>        many left to take. ')
<a name="434"></a>    
<a name="435"></a>    /* Are we the Collective for obj? [COLLECTIVE EXTENSION] */
<a name="436"></a>    isCollectiveFor(obj) 
<a name="437"></a>    {         
<a name="438"></a>        
<a name="439"></a>        /* We are not the Collective for obj if obj */
<a name="440"></a>        if(dispensedObjs &amp;&amp; valToList(dispensedObjs).indexOf(obj) != nil)
<a name="441"></a>            return true;
<a name="442"></a>        
<a name="443"></a>        if(dispensedClass &amp;&amp; obj.ofKind(dispensedClass))
<a name="444"></a>            return true;
<a name="445"></a>             
<a name="446"></a>        return nil; 
<a name="447"></a>    
<a name="448"></a>    }
<a name="449"></a>    
<a name="450"></a>    /* The TakeFrom action should always act on us, the Collective. [COLLECTIVE EXTENSION] */
<a name="451"></a>    collectiveAction(np, cmd) 
<a name="452"></a>    { 
<a name="453"></a>        return cmd.action == TakeFrom; 
<a name="454"></a>    }
<a name="455"></a>    
<a name="456"></a>    /* Overidden for COLLECTIVE EXTENSION */
<a name="457"></a>    preinitThing
<a name="458"></a>    {
<a name="459"></a>        inherited();
<a name="460"></a>        
<a name="461"></a>        /* 
<a name="462"></a>         *   Force an initial calculation of our numLeft property, the
<a name="463"></a>         *   number of items we have left to dispense.
<a name="464"></a>         */
<a name="465"></a>        canDispense();
<a name="466"></a>    }
<a name="467"></a>;
<a name="468"></a>
</pre>
<div class=ftr>Adv3Lite Library Reference Manual<br>Generated on 08/12/2025 from adv3Lite version 2.2.2</div>
</body>
</html>
