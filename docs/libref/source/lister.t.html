<html><head><link rel=stylesheet type="text/css" href="../libref.css"><title>lister.t</title></head><body>
<table class=ban><tr><td><h1>lister.t</h1><td align=right><a href="../file/lister.t.html">documentation</a></table><pre>
<a name="1"></a>#charset "us-ascii"
<a name="2"></a>#include "advlite.h"
<a name="3"></a>
<a name="4"></a>
<a name="5"></a>/*
<a name="6"></a> *   ***************************************************************************
<a name="7"></a> *   lister.t
<a name="8"></a> *
<a name="9"></a> *   This module forms part of the adv3Lite library (c) 2012-13 Eric Eve, and is
<a name="10"></a> *   substantially borrowed from the Mercury library (c) 2012 Michael J.
<a name="11"></a> *   Roberts.
<a name="12"></a> */
<a name="13"></a>
<a name="14"></a>
<a name="15"></a>/*
<a name="16"></a> *   Lister is the class that displays lists of objects.  This is used in
<a name="17"></a> *   room descriptions, inventory lists, and EXAMINE descriptions of
<a name="18"></a> *   objects (to show the examined object's contents).
<a name="19"></a> *   
<a name="20"></a> *   Showing a listing is basically a function call.  The reason we make a
<a name="21"></a> *   whole class out of it is that we provide a number of options, and a
<a name="22"></a> *   class is a convenient way to specify options.  The options are simply
<a name="23"></a> *   defined as properties of a lister object, so to create a certain kind
<a name="24"></a> *   of list, you just set up a Lister instance with the desired options.
<a name="25"></a> *   We provide pre-defined Lister objects for the common library listing
<a name="26"></a> *   types, but games can create their own custom list types by creating
<a name="27"></a> *   their own Lister objects for different sets of options.  
<a name="28"></a> */
<a name="29"></a>class Lister: object
<a name="30"></a>    /*
<a name="31"></a>     *   Show a list of objects.  'lst' is the list of objects to show;
<a name="32"></a>     *   'paraCnt' is the number of paragraph-style descriptions that we've
<a name="33"></a>     *   already shown as part of this description.
<a name="34"></a>     *
<a name="35"></a>     *   Note that many specific listers replaced the 'paraCnt' parameter with a
<a name="36"></a>     *   more useful 'parent' parameter, containing the identity of the object
<a name="37"></a>     *   whose contents are being listed.
<a name="38"></a>     */
<a name="39"></a>    show(lst, paraCnt, paraBrk = true)
<a name="40"></a>    {
<a name="41"></a>        /* get the subset that passes our 'listed' test */
<a name="42"></a>        lst = lst.subset({ o: listed(o) });
<a name="43"></a>        
<a name="44"></a>        /* if we have any items, show them */
<a name="45"></a>        if (lst.length() &gt; 0)
<a name="46"></a>        {
<a name="47"></a>            /* sort into listing order */
<a name="48"></a>            lst = lst.sort(SortAsc, { a, b: listOrder(a) - listOrder(b) });
<a name="49"></a>            
<a name="50"></a>            /* 
<a name="51"></a>             *   The list is plural if it has multiple items, or a single item
<a name="52"></a>             *   that has plural usage.
<a name="53"></a>             */
<a name="54"></a>            local pl = (lst.length() &gt; 1 || lst[1].plural);
<a name="55"></a>            
<a name="56"></a>            /* Show the list prefix */
<a name="57"></a>            showListPrefix(lst, pl, paraCnt);
<a name="58"></a>            
<a name="59"></a>            /* show the items */
<a name="60"></a>            showList(lst, pl, paraCnt);
<a name="61"></a>            
<a name="62"></a>            /* Show the list suffix. */
<a name="63"></a>            showListSuffix(lst, pl, paraCnt);
<a name="64"></a>            
<a name="65"></a>            /* add a paragraph break at the end, if it's requested */
<a name="66"></a>            if(paraBrk)
<a name="67"></a>                "&lt;.p&gt;";
<a name="68"></a>        }
<a name="69"></a>        else
<a name="70"></a>            showListEmpty(paraCnt);
<a name="71"></a>    }
<a name="72"></a>    
<a name="73"></a>    showListPrefix(lst, pl, paraCnt)  { }
<a name="74"></a>    
<a name="75"></a>    showListSuffix(lst, pl, paraCnt)  { }
<a name="76"></a>    
<a name="77"></a>    showListEmpty(paraCnt)  { }
<a name="78"></a>    
<a name="79"></a>     
<a name="80"></a>    /*
<a name="81"></a>     *   Should 'obj' be listed in this list?  Returns true if so, nil if not.
<a name="82"></a>     *   By default, we list any object whose 'listed' property is true.
<a name="83"></a>     */
<a name="84"></a>    listed(obj) { return obj.listed; }
<a name="85"></a>    
<a name="86"></a>    /*
<a name="87"></a>     *   Get an item's sorting order.  This returns an integer that gives the
<a name="88"></a>     *   relative position in the list; we order the list in ascending order of
<a name="89"></a>     *   this value.  By default, we return the 'listOrder' property of the
<a name="90"></a>     *   object.
<a name="91"></a>     */
<a name="92"></a>    listOrder(obj) { return obj.listOrder; }
<a name="93"></a>    
<a name="94"></a>    
<a name="95"></a>    
<a name="96"></a>    /* 
<a name="97"></a>     *   Return a string containing what this lister would display, minus the
<a name="98"></a>     *   terminating paragraph break.
<a name="99"></a>     */
<a name="100"></a>    buildList(lst)
<a name="101"></a>    {
<a name="102"></a>        local str = gOutStream.captureOutput({: show(lst, 0, nil) });
<a name="103"></a>        
<a name="104"></a>        return str;
<a name="105"></a>    }
<a name="106"></a>    
<a name="107"></a>;
<a name="108"></a>
<a name="109"></a>/* 
<a name="110"></a> *   An Item Lister is a lister used for listing physical items. Notice that
<a name="111"></a> *   most of the specifics of the listers defined below are language-specific,
<a name="112"></a> *   and so are defined in the language-specific part of the library (e.g. in
<a name="113"></a> *   english.t).
<a name="114"></a> */
<a name="115"></a>class ItemLister: Lister
<a name="116"></a>    
<a name="117"></a>    /*
<a name="118"></a>     *   Show a list of objects.  'lst' is the list of objects to show; 'parent'
<a name="119"></a>     *   parameter is the object whose contents are being listed, 'paraBrk'
<a name="120"></a>     *   defines whether or not we want a paragraph break after the list.
<a name="121"></a>     */
<a name="122"></a>    show(lst, parent, paraBrk = true)
<a name="123"></a>    {
<a name="124"></a>        /* Carry out the inherited handling */
<a name="125"></a>        inherited(lst, parent, paraBrk);
<a name="126"></a>        
<a name="127"></a>        /* Note that every item in our list has been mentioned and seen, provided it's listed. */
<a name="128"></a>        foreach(local cur in lst)
<a name="129"></a>        {
<a name="130"></a>            if(listed(cur))
<a name="131"></a>            {
<a name="132"></a>                cur.mentioned = true;
<a name="133"></a>                cur.noteSeen();
<a name="134"></a>            }
<a name="135"></a>        }        
<a name="136"></a>    }
<a name="137"></a>    
<a name="138"></a>    showList(lst, pl, parent)   
<a name="139"></a>    {    
<a name="140"></a>        lst = findListGroups(lst);
<a name="141"></a>        
<a name="142"></a>        /* 
<a name="143"></a>         *   If our groupTab table is empty, there are no ListGroups involved in our list, so we can
<a name="144"></a>         *   just show a simple list of items.
<a name="145"></a>         */
<a name="146"></a>        if(groupTab.getEntryCount() == 0)
<a name="147"></a>            showSimpleList(lst);
<a name="148"></a>        
<a name="149"></a>        /* Otherwise we'll need to use the more commplex list handling. */
<a name="150"></a>        else
<a name="151"></a>            showComplexList(lst, pl, parent);
<a name="152"></a>    }
<a name="153"></a>    
<a name="154"></a>    /* 
<a name="155"></a>     *   Show a simple list of things. This must be defined in the language-specific part of the
<a name="156"></a>     *   library.
<a name="157"></a>     */
<a name="158"></a>    showSimpleList(lst)     {  }
<a name="159"></a>    
<a name="160"></a>    /* Our LookUp table of any ListGroups we need to work with. */
<a name="161"></a>    groupTab = nil
<a name="162"></a>    
<a name="163"></a>    findListGroups(lst)
<a name="164"></a>    {
<a name="165"></a>        /* Create a new LookupTable for our table of ListGroups */
<a name="166"></a>        groupTab =  new LookupTable;
<a name="167"></a>        
<a name="168"></a>        /* 
<a name="169"></a>         *   For every item we're due to list, if it has any ListGroups listed in its listWith
<a name="170"></a>         *   property, create or update an entry for that ListGroup and the item it lists in our
<a name="171"></a>         *   table of ListGroups.
<a name="172"></a>         */
<a name="173"></a>        foreach(local item in lst)
<a name="174"></a>        {
<a name="175"></a>            foreach(local grp in valToList(item.listWith))
<a name="176"></a>                groupTab[grp] = valToList(groupTab[grp]) + item;                    
<a name="177"></a>        }        
<a name="178"></a>      
<a name="179"></a>        /* Get a list of the groups that have been added to groupTab. */
<a name="180"></a>        local grpList = groupTab.keysToList();
<a name="181"></a>        
<a name="182"></a>        /* 
<a name="183"></a>         *   Then remove from the list group table entries for any ListGroups that don't match the
<a name="184"></a>         *   minimum number of items they should list.
<a name="185"></a>         */
<a name="186"></a>        foreach (local grp in grpList)
<a name="187"></a>        {
<a name="188"></a>            if(groupTab[grp].length &lt; grp.minGroupSize)
<a name="189"></a>                groupTab.removeElement(grp);
<a name="190"></a>        }
<a name="191"></a>        
<a name="192"></a>        /* 
<a name="193"></a>         *   If there's nothing left in the list group table, there's nothing more to do here, so
<a name="194"></a>         *   simply return our list of items unchanged.
<a name="195"></a>         */
<a name="196"></a>        if(groupTab.getEntryCount() == 0)
<a name="197"></a>            return lst;
<a name="198"></a>        
<a name="199"></a>        /* 
<a name="200"></a>         *   Get an updated list of the groups that are left, in descending order of priority and
<a name="201"></a>         *   the number of items they relate to.
<a name="202"></a>         */
<a name="203"></a>        grpList = groupTab.keysToList().sort(SortAsc, {a, b: b.priority + groupTab[b].length - 
<a name="204"></a>                                             a.priority - groupTab[a].length});
<a name="205"></a>        
<a name="206"></a>        local listed = [];
<a name="207"></a>        
<a name="208"></a>        /* 
<a name="209"></a>         *   Check for items that appear in more than one ListGroup. Remove their association from
<a name="210"></a>         *   the groupTab table, and if that leaves a ListGroup with too few items, remove that too.
<a name="211"></a>         */
<a name="212"></a>        foreach(local grp in grpList)
<a name="213"></a>        {
<a name="214"></a>            /* Note the items covered by the current ListGroup grp */
<a name="215"></a>            local items = groupTab[grp];
<a name="216"></a>            
<a name="217"></a>            /* 
<a name="218"></a>             *   Remove items from the list that would already have been listed in previous
<a name="219"></a>             *   ListGroups.
<a name="220"></a>             */
<a name="221"></a>            items = items.subset({x: listed.indexOf(x) == nil});
<a name="222"></a>            
<a name="223"></a>            /*  Update the groupTab entry for this group with the revised list of items. */
<a name="224"></a>            groupTab[grp] = items;                       
<a name="225"></a>                  
<a name="226"></a>            /* 
<a name="227"></a>             *   If that leaves the group with too few items for the group to be used, remove it
<a name="228"></a>             *   from our group table.
<a name="229"></a>             */
<a name="230"></a>            if(items.length &lt; grp.minGroupSize)
<a name="231"></a>                groupTab.removeElement(grp);
<a name="232"></a>            
<a name="233"></a>            /* Otherwise, add its items to the list of items that will be listed by ListGroups. */
<a name="234"></a>            else
<a name="235"></a>                listed = listed.appendUnique(items); 
<a name="236"></a>        }
<a name="237"></a>        
<a name="238"></a>        /* 
<a name="239"></a>         *   Remove the list of items to be listed by ListGroups from the list of items to be listed
<a name="240"></a>         *   and add the ListGroups in their place.
<a name="241"></a>         */
<a name="242"></a>        lst = lst - listed + groupTab.keysToList();
<a name="243"></a>        
<a name="244"></a>        /* Sort the resulting list in ascending order of listOrder. */
<a name="245"></a>        lst = lst.sort(SortAsc, {a, b: a.listOrder - b.listOrder});
<a name="246"></a>        
<a name="247"></a>        /* Return the resulting list. */
<a name="248"></a>        return lst;
<a name="249"></a>    }
<a name="250"></a>    
<a name="251"></a>    /* Show a list of items that may be a list of Things and ListGroups */
<a name="252"></a>    showComplexList(lst, pl, parent)
<a name="253"></a>    {
<a name="254"></a>        /* Note the length of the list. */
<a name="255"></a>        local len = lst.length();
<a name="256"></a>        
<a name="257"></a>        /* Iterate through the list. */
<a name="258"></a>        for(local i = 1, local item in lst;; ++ i)    
<a name="259"></a>        { 
<a name="260"></a>            /* If we're reaching the final item in the list, prepend ' and ' */
<a name="261"></a>            if(i == len &amp;&amp; i &gt; 1)
<a name="262"></a>                DMsg(list and, ' and ');
<a name="263"></a>            
<a name="264"></a>            /* Note the punctuation that should follow this item. */
<a name="265"></a>            local punct = ', ';
<a name="266"></a>            
<a name="267"></a>            /* If the item is a Thimg, simpyl display its listName. */
<a name="268"></a>            if(item.ofKind(Thing))
<a name="269"></a>            {                
<a name="270"></a>                "&lt;&lt;listName(item)&gt;&gt;";
<a name="271"></a>            }
<a name="272"></a>            
<a name="273"></a>            /* 
<a name="274"></a>             *   If it's a ListGroup, get the ListGroup to list its contents and then change the
<a name="275"></a>             *   following punctuation to a semicolon.
<a name="276"></a>             */
<a name="277"></a>            if(item.ofKind(ListGroup))
<a name="278"></a>            {
<a name="279"></a>                item.showList(self, groupTab[item]);
<a name="280"></a>                punct = '; ';
<a name="281"></a>            }
<a name="282"></a>            
<a name="283"></a>            /* If we haven't reach the end of the list, add the appropriate punctuation. */
<a name="284"></a>            if(i &lt; len)
<a name="285"></a>                say(punct);
<a name="286"></a>        }   
<a name="287"></a>            
<a name="288"></a>    }
<a name="289"></a>    
<a name="290"></a>    /* 
<a name="291"></a>     *   The property on a Thing-derived container to test whether its contents
<a name="292"></a>     *   should be listed when listing with this lister
<a name="293"></a>     */
<a name="294"></a>    contentsListedProp = &amp;contentsListed
<a name="295"></a>    
<a name="296"></a>    /* 
<a name="297"></a>     *   Flag, so we want to list contents of contents when using this lister;
<a name="298"></a>     *   by default we do.
<a name="299"></a>     */
<a name="300"></a>    listRecursively = (gActor == gPlayerChar)   
<a name="301"></a>;
<a name="302"></a>
<a name="303"></a>
<a name="304"></a>/*
<a name="305"></a> *   lookLister displays a list of miscellaneous objects in a room description.
<a name="306"></a> */
<a name="307"></a>lookLister: ItemLister
<a name="308"></a>    /* is the object listed in a LOOK AROUND description? */
<a name="309"></a>    listed(obj) { return obj.lookListed &amp;&amp; !obj.isHidden; }
<a name="310"></a>;
<a name="311"></a>
<a name="312"></a>/* 
<a name="313"></a> *   lookContentsLister is used to display a list of the contents of objects in
<a name="314"></a> *   a room description.
<a name="315"></a> */
<a name="316"></a>lookContentsLister: ItemLister
<a name="317"></a>    /* is the object listed in a LOOK AROUND description? */
<a name="318"></a>    listed(obj) { return obj.lookListed &amp;&amp; !obj.isHidden; }
<a name="319"></a>    
<a name="320"></a>    contentsListedProp = &amp;contentsListedInLook
<a name="321"></a>;
<a name="322"></a>
<a name="323"></a>/*
<a name="324"></a> *   inventoryLister displays an inventory listing in WIDE format.
<a name="325"></a> */
<a name="326"></a>inventoryLister: ItemLister
<a name="327"></a>    /* is the object listed in an inventory list? */
<a name="328"></a>    listed(obj) { return obj.inventoryListed &amp;&amp; !obj.isHidden; }
<a name="329"></a>;
<a name="330"></a>
<a name="331"></a>/* wornLister displays a list of items being worn. */
<a name="332"></a>wornLister: ItemLister
<a name="333"></a>     /* is the object listed in an inventory list? */
<a name="334"></a>    listed(obj) { return obj.inventoryListed &amp;&amp; !obj.isHidden; }
<a name="335"></a>;
<a name="336"></a>
<a name="337"></a>/*
<a name="338"></a> *   inventoryTallLister for displaying an inventory list in TALL format.
<a name="339"></a> */
<a name="340"></a>inventoryTallLister: ItemLister
<a name="341"></a>    /* is the object listed in an inventory list? */
<a name="342"></a>    listed(obj) { return obj.inventoryListed &amp;&amp; !obj.isHidden; }
<a name="343"></a>    
<a name="344"></a>     showList(lst, parent, paraBrk = true)
<a name="345"></a>    {
<a name="346"></a>        /* list the inventory using the inventory tall format. */
<a name="347"></a>        showContentsTall(lst, parent, paraBrk);
<a name="348"></a>        
<a name="349"></a>        
<a name="350"></a>        /* Ensure the indentation level is reset to zero once we've finished listing */
<a name="351"></a>        indentLevel = 1;   
<a name="352"></a>        
<a name="353"></a>    }
<a name="354"></a>    
<a name="355"></a>    
<a name="356"></a>    /* 
<a name="357"></a>     *   List the player's inventory in tall format, i.e., as a columnar list with each item on a
<a name="358"></a>     *   new line. This method may call itself recursively to list subcontents (such as the visible
<a name="359"></a>     *   contents of any containers in the player character's inventory).
<a name="360"></a>     */
<a name="361"></a>    showContentsTall(lst, parent, paraBrk = true) 
<a name="362"></a>    {
<a name="363"></a>        foreach(local cur in lst)
<a name="364"></a>        {
<a name="365"></a>            /* Carry out the indenting for sublisting contents */
<a name="366"></a>            for(local i in 1..indentLevel)            
<a name="367"></a>                "\t";
<a name="368"></a>            
<a name="369"></a>            /* Display the appropriate name for the listed item */
<a name="370"></a>            say(listName(cur));
<a name="371"></a>            
<a name="372"></a>            /* Move to a new line */
<a name="373"></a>            "\n";
<a name="374"></a>            
<a name="375"></a>            /* Note that every item in our list has been mentioned and seen */
<a name="376"></a>            cur.mentioned = true;
<a name="377"></a>            cur.noteSeen();  
<a name="378"></a>            
<a name="379"></a>            /* 
<a name="380"></a>             *   If we want to list recursively and we haven't yet reached out maximum indentation
<a name="381"></a>             *   (i.e., nesting) level, then build a list of subcontents and then display it.
<a name="382"></a>             */
<a name="383"></a>            if(listRecursively &amp;&amp; indentLevel &lt; maxIndentLevel)
<a name="384"></a>            {
<a name="385"></a>                /* 
<a name="386"></a>                 *   Get a list of the current item's listable contents. If we can't see in this is
<a name="387"></a>                 *   an empty list.
<a name="388"></a>                 */
<a name="389"></a>                local subList = (cur.contType == In &amp;&amp; !cur.canSeeIn) 
<a name="390"></a>                    ? [] : cur.contents.subset({o: listed(o) });
<a name="391"></a>                
<a name="392"></a>                /*   If we have an open or transparent subcontainer, add its contents. */ 
<a name="393"></a>                if(cur.remapIn &amp;&amp; cur.remapIn.canSeeIn)
<a name="394"></a>                    subList += cur.remapIn.contents.subset({o: listed(o) });
<a name="395"></a>                
<a name="396"></a>                /*   If we have a subsurface, add its contents. */ 
<a name="397"></a>                if(cur.remapOn)
<a name="398"></a>                    subList += cur.remapOn.contents.subset({o: listed(o) });
<a name="399"></a>                
<a name="400"></a>                
<a name="401"></a>                /* 
<a name="402"></a>                 *   If this list isn't empty, then display this list of subcontents as a column of
<a name="403"></a>                 *   items indented under their containing item.
<a name="404"></a>                 */
<a name="405"></a>                if(subList.length &gt; 0)
<a name="406"></a>                {
<a name="407"></a>                    /* increment the indentation level. */
<a name="408"></a>                    indentLevel++;
<a name="409"></a>                    
<a name="410"></a>                    /* sort the list of subcontents in ascending order of their listOrder property */
<a name="411"></a>                    subList = subList.sort(true, {x, y: y.listOrder - x.listOrder});
<a name="412"></a>                    
<a name="413"></a>                    /* call this method recursively to list the subcontents. */
<a name="414"></a>                    showContentsTall(subList, cur, paraBrk);
<a name="415"></a>                    
<a name="416"></a>                    /* decrement the indentation level once we've finished listing. */
<a name="417"></a>                    indentLevel-- ;
<a name="418"></a>                    
<a name="419"></a>                }
<a name="420"></a>            }
<a name="421"></a>        }
<a name="422"></a>        
<a name="423"></a>    }    
<a name="424"></a>    
<a name="425"></a>    
<a name="426"></a>    /* 
<a name="427"></a>     *   A version of the listName method that doesn't list an items contents in parenthesis after
<a name="428"></a>     *   its name, which would be inappropriate to the tall inventory format.
<a name="429"></a>     */
<a name="430"></a>    listName(o)
<a name="431"></a>    {
<a name="432"></a>        /* 
<a name="433"></a>         *   When we're doing a tall inventory listing we don't want to list sucontents after the
<a name="434"></a>         *   name of each item, so we store the current value of showSubListing, then set
<a name="435"></a>         *   showSublisting to nil before carrying out the inherited handling, and then finally
<a name="436"></a>         *   restore the original value of ShowSubListing.
<a name="437"></a>         */
<a name="438"></a>        
<a name="439"></a>        local ssl = showSubListing;
<a name="440"></a>        
<a name="441"></a>        showSubListing = nil;
<a name="442"></a>        
<a name="443"></a>        local lnam = inherited(o);
<a name="444"></a>        
<a name="445"></a>        showSubListing = ssl;
<a name="446"></a>        
<a name="447"></a>        return lnam;          
<a name="448"></a>        
<a name="449"></a>    }
<a name="450"></a>    
<a name="451"></a>    /* The current indentation level for listing subcontents recursively */
<a name="452"></a>    indentLevel = 1
<a name="453"></a>    
<a name="454"></a>    /* The maximum level of indentation we want to allow for listed nested subcontents. */
<a name="455"></a>    maxIndentLevel = 5
<a name="456"></a>    
<a name="457"></a>    /* 
<a name="458"></a>     *   The property on a Thing-derived container to test whether its contents
<a name="459"></a>     *   should be listed when listing with this lister
<a name="460"></a>     */
<a name="461"></a>    contentsListedProp = &amp;contentsListed
<a name="462"></a>    
<a name="463"></a>    /* 
<a name="464"></a>     *   Flag, so we want to list contents of contents when using this lister;
<a name="465"></a>     *   by default we do if the actor is the player character.
<a name="466"></a>     */
<a name="467"></a>    listRecursively = (gActor == gPlayerChar)
<a name="468"></a>    
<a name="469"></a>    showListPrefix(lst, pl, paraCnt)  { DMsg(list tall prefix, '\n{I} {am} carrying:\n '); }
<a name="470"></a>    
<a name="471"></a>    showListSuffix(lst, pl, paraCnt)  { }
<a name="472"></a>    
<a name="473"></a>    showListEmpty(paraCnt)  { DMsg(list tall empty, '\n{I} {am} empty-handed. '); }
<a name="474"></a>    
<a name="475"></a>;
<a name="476"></a>
<a name="477"></a>
<a name="478"></a>
<a name="479"></a>
<a name="480"></a>
<a name="481"></a>/*
<a name="482"></a> *   descContentsLister displays a list of miscellaneous contents of an object
<a name="483"></a> *   being examined.  
<a name="484"></a> */
<a name="485"></a>descContentsLister: ItemLister
<a name="486"></a>    /* is the object listed in an EXAMINE description of its container? */
<a name="487"></a>    listed(obj) { return obj.examineListed &amp;&amp; !obj.isHidden; }
<a name="488"></a>
<a name="489"></a>    contentsListedProp = &amp;contentsListedInExamine
<a name="490"></a>;
<a name="491"></a>
<a name="492"></a>/* 
<a name="493"></a> *   openingContentsLister displays the contents of an openable container when
<a name="494"></a> *   it is first opened.
<a name="495"></a> */
<a name="496"></a>openingContentsLister: ItemLister
<a name="497"></a>    /* is the object listed in an EXAMINE description of its container? */
<a name="498"></a>    listed(obj) { return obj.examineListed &amp;&amp; !obj.isHidden; }
<a name="499"></a>    
<a name="500"></a>    /* 
<a name="501"></a>     *   We don't want recursive listing with the openingContentsLister, since
<a name="502"></a>     *   this can produce odd results.
<a name="503"></a>     */
<a name="504"></a>    listRecursively = nil
<a name="505"></a>;
<a name="506"></a>
<a name="507"></a>/* 
<a name="508"></a> *   lookInLister is used to list the contents of an object in response to LOOK
<a name="509"></a> *   IN/UNDER/BEHIND
<a name="510"></a> */
<a name="511"></a>lookInLister: ItemLister
<a name="512"></a>    /* 
<a name="513"></a>     *   is the object listed in a SEARCH/LOOK IN/UNDER/BEHIND description of
<a name="514"></a>     *   its container?
<a name="515"></a>     */
<a name="516"></a>    listed(obj) { return obj.searchListed &amp;&amp; !obj.isHidden; }
<a name="517"></a>
<a name="518"></a>    contentsListedProp = &amp;contentsListedInSearch
<a name="519"></a>
<a name="520"></a>;
<a name="521"></a>
<a name="522"></a>/* A lister used to list the items attached to a SimpleAttachable */
<a name="523"></a>simpleAttachmentLister: ItemLister
<a name="524"></a>    /* an object is listed if it's attached */
<a name="525"></a>    listed(obj) { return obj.attachedTo != nil &amp;&amp; !obj.isHidden; }
<a name="526"></a>    
<a name="527"></a>;
<a name="528"></a>
<a name="529"></a>/*  A lister used to list the items plugged into a PlugAttachable */
<a name="530"></a>plugAttachableLister: simpleAttachmentLister
<a name="531"></a>;
<a name="532"></a>
<a name="533"></a>/* 
<a name="534"></a> *   A lister that can be readily customized to tailor the text before and after
<a name="535"></a> *   a list of miscellaneous items in a room description.
<a name="536"></a> */
<a name="537"></a>class CustomRoomLister: ItemLister
<a name="538"></a>    
<a name="539"></a>    /* is the object listed in a LOOK AROUND description? */
<a name="540"></a>    listed(obj) { return obj.lookListed &amp;&amp; !obj.isHidden; }
<a name="541"></a>    
<a name="542"></a>    /* 
<a name="543"></a>     *   In the simple form of the constructor, we just supply a string that
<a name="544"></a>     *   will form the prefix string for the lister. In the more sophisticated
<a name="545"></a>     *   form we can supply an additsion argument that's an anonymous method or
<a name="546"></a>     *   function that's used to show the list prefix or suffix, or else just
<a name="547"></a>     *   the suffix string.
<a name="548"></a>     */
<a name="549"></a>    construct(prefix, prefixMethod:?, suffix:?, suffixMethod:?)
<a name="550"></a>    {
<a name="551"></a>        prefix_ = prefix;
<a name="552"></a>        
<a name="553"></a>        if(prefixMethod != nil)
<a name="554"></a>            setMethod(&amp;showListPrefix, prefixMethod);
<a name="555"></a>        
<a name="556"></a>        if(suffix != nil)
<a name="557"></a>            suffix_ = suffix;
<a name="558"></a>        
<a name="559"></a>        if(suffixMethod != nil)
<a name="560"></a>            setMethod(&amp;showListSuffix, suffixMethod);
<a name="561"></a>    }
<a name="562"></a>    
<a name="563"></a>    prefix_ = nil
<a name="564"></a>    suffix_ = '. '
<a name="565"></a>    
<a name="566"></a>    showListPrefix(lst, pl, irName)  
<a name="567"></a>    { 
<a name="568"></a>        "&lt;.p&gt;&lt;&lt;prefix_&gt;&gt; ";
<a name="569"></a>    }
<a name="570"></a>    
<a name="571"></a>    showListSuffix(lst, pl, irName)  
<a name="572"></a>    { 
<a name="573"></a>        "&lt;&lt;suffix_&gt;&gt;";
<a name="574"></a>    }
<a name="575"></a>    
<a name="576"></a>    showSubListing = (gameMain.useParentheticalListing)
<a name="577"></a>;
<a name="578"></a>
<a name="579"></a>
<a name="580"></a>/*
<a name="581"></a> *   -----------------------------------------------------------------------------------
<a name="582"></a> *
<a name="583"></a> *   Various types of ListGroup
<a name="584"></a> *
<a name="585"></a> *   These can be defined on the listWith property of miscellaneous items to be listed in a room,
<a name="586"></a> *   container or inventory listing to group items together.
<a name="587"></a> */
<a name="588"></a>
<a name="589"></a>
<a name="590"></a>/* 
<a name="591"></a> *   Abstract base class for all kinds of ListGroup. Game code will use one of more of its
<a name="592"></a> *   subclasses defined below.
<a name="593"></a> */
<a name="594"></a>class ListGroup: object
<a name="595"></a>    
<a name="596"></a>    /* 
<a name="597"></a>     *   The minimum number of objects in the list to be listed belonging to this ListGroup for this
<a name="598"></a>     *   ListGroup to be used to list them. By default this is two.
<a name="599"></a>     */
<a name="600"></a>    minGroupSize = 2    
<a name="601"></a>    
<a name="602"></a>    
<a name="603"></a>    /* Show a list of items (lst) using lister. */
<a name="604"></a>    showList(lister, lst)
<a name="605"></a>    {
<a name="606"></a>        showSimpleList(lister, lst);
<a name="607"></a>    }
<a name="608"></a>    
<a name="609"></a>    /* 
<a name="610"></a>     *   Show a simple list of items (lst) using lister. The method of doing so must be provided in
<a name="611"></a>     *   the language-dependent part of the libary.
<a name="612"></a>     */    
<a name="613"></a>    showSimpleList(lister, lst)   {}
<a name="614"></a>    
<a name="615"></a>    /* 
<a name="616"></a>     *   Our own list order, relative to the listOrder of other items in the list to be listed that
<a name="617"></a>     *   do not belong to this ListGroup.
<a name="618"></a>     */
<a name="619"></a>    listOrder = 100
<a name="620"></a>    
<a name="621"></a>    /* 
<a name="622"></a>     *   Normally if an item to be listed belongs to more than one ListGroup, we use the ListGroup
<a name="623"></a>     *   that would contain the largest number of items. We can override this by assigning a higher
<a name="624"></a>     *   priority to the ListGroup so that the highet priority ListGroup will be use. This should be
<a name="625"></a>     *   a number that's large compared with the number of items in any one group so we use a
<a name="626"></a>     *   default of 100. Game code may wish to assign priorities in steps of a hundred.
<a name="627"></a>     */
<a name="628"></a>    priority = 100
<a name="629"></a>;
<a name="630"></a>
<a name="631"></a>
<a name="632"></a>/* A ListGroupSorted simply lists its items in ascending listOrder sequence within the group. */
<a name="633"></a>class ListGroupSorted: ListGroup
<a name="634"></a>    
<a name="635"></a>    /* 
<a name="636"></a>     *   The compareGroupItems method defines how items will be sorted within the ListGroup. By
<a name="637"></a>     *   default we sort on their groupOrder, which by default is the same as their listOrder,  but
<a name="638"></a>     *   we could define an alternative custom property or some other means of sorting if we so
<a name="639"></a>     *   wished.
<a name="640"></a>     */
<a name="641"></a>    compareGroupItems (a, b) { return a.groupOrder - b.groupOrder; }    
<a name="642"></a>    
<a name="643"></a>    /* Show our list (lst) of items using lister. */
<a name="644"></a>    showList(lister, lst)
<a name="645"></a>    {
<a name="646"></a>        /* First sort our items. */
<a name="647"></a>        lst = sortList(lst);
<a name="648"></a>        
<a name="649"></a>        /* Then show a simple list of them using lister. */
<a name="650"></a>        showSimpleList(lister, lst);
<a name="651"></a>    }
<a name="652"></a>    
<a name="653"></a>    /* Sort our list of items. By default we do so in their listOrder order. */
<a name="654"></a>    sortList(lst) { return lst.sort(SortAsc, {a, b: compareGroupItems(a, b)});  }
<a name="655"></a>;
<a name="656"></a>
<a name="657"></a>
<a name="658"></a>/* 
<a name="659"></a> *   ListGroupSuffixPrefix is a type of ListGroupSorted to which we can prepend or append custom
<a name="660"></a> *   text, e.g. "keys: "
<a name="661"></a> */
<a name="662"></a>class ListGroupSuffixPrefix: ListGroupSorted
<a name="663"></a>    
<a name="664"></a>    /* 
<a name="665"></a>     *   The prefix text we want to display before our list of items, if any. This is for game code
<a name="666"></a>     *   to define.
<a name="667"></a>     */
<a name="668"></a>    groupPrefix = nil
<a name="669"></a>        
<a name="670"></a>    /* 
<a name="671"></a>     *   The suffix text we want to display after our list of items, if any. This is for game code
<a name="672"></a>     *   to define.
<a name="673"></a>     */
<a name="674"></a>    groupSuffix = nil
<a name="675"></a>    
<a name="676"></a>    /*  
<a name="677"></a>     *   Show our group prefix. By default we display a apelled out count of the number of items
<a name="678"></a>     *   this group is going to list followed by our prefix text, but game code can override if some
<a name="679"></a>     *   other format is wanted.
<a name="680"></a>     */
<a name="681"></a>    showGroupPrefix(pov, lst)         
<a name="682"></a>    { 
<a name="683"></a>        /* Spell out the number of items we're about to list. */
<a name="684"></a>        "&lt;&lt;spellNumber(lst.length)&gt;&gt; ";
<a name="685"></a>        
<a name="686"></a>        /* Then display our group prefix/ */
<a name="687"></a>        display(&amp;groupPrefix); 
<a name="688"></a>    }
<a name="689"></a>    
<a name="690"></a>    /* Display our group suffix. */
<a name="691"></a>    showGroupSuffix(pov, lst) { display(&amp;groupSufffix); }
<a name="692"></a>          
<a name="693"></a>    /* Show our list of items (lst) using lister. */
<a name="694"></a>    showList(lister, lst)
<a name="695"></a>    {
<a name="696"></a>        /* Display our group prefix. */
<a name="697"></a>        showGroupPrefix(gActor, lst); 
<a name="698"></a>        
<a name="699"></a>        /* Sort out list */
<a name="700"></a>        lst = sortList(lst);
<a name="701"></a>        
<a name="702"></a>        /* Output a apace to separate the list from our prefix. */
<a name="703"></a>        " ";
<a name="704"></a>        
<a name="705"></a>        /* Show a simple list of our items (lst) using lister. */
<a name="706"></a>        showSimpleList(lister, lst);
<a name="707"></a>        
<a name="708"></a>        /* Output another space to separate the list from its suffix */
<a name="709"></a>        " ";
<a name="710"></a>        
<a name="711"></a>        /* Show the group suffix. */
<a name="712"></a>        showGroupSuffix(gActor, lst);
<a name="713"></a>    }        
<a name="714"></a>        
<a name="715"></a>;
<a name="716"></a>
<a name="717"></a>
<a name="718"></a>/* 
<a name="719"></a> *   ListGroupParem introduces the list with a summary of what it contains (e.g., 'three keys') and
<a name="720"></a> *   then lists its individual items in parentheses (e.g. 'three keys (a large key, a brass key, and
<a name="721"></a> *   a tiny silver key).
<a name="722"></a> */
<a name="723"></a>class ListGroupParen: ListGroupSorted
<a name="724"></a>    
<a name="725"></a>    /* Show the summmary prefix, e.g. "three keys". */
<a name="726"></a>    showGroupCountName(lst)
<a name="727"></a>    {
<a name="728"></a>        "&lt;&lt;spellNumber(lst.length)&gt;&gt; &lt;&lt;pluralName&gt;&gt;";
<a name="729"></a>    }     
<a name="730"></a>    
<a name="731"></a>    /* The plural name to use to summarise the items in the group, e.g., 'keys' */
<a name="732"></a>    pluralName = ''
<a name="733"></a>    
<a name="734"></a>    /* Show our list of items (lst) using lister. */
<a name="735"></a>    showList(lister, lst)
<a name="736"></a>    {
<a name="737"></a>        /* Start with our summary description, e.g., 'three keys' */
<a name="738"></a>        showGroupCountName(lst);
<a name="739"></a>        
<a name="740"></a>        /* Sort our list of items. */
<a name="741"></a>        lst = sortList(lst);
<a name="742"></a>        
<a name="743"></a>        /* Display the opening parenthesis. */
<a name="744"></a>        " (";
<a name="745"></a>        
<a name="746"></a>        /* Show a simple list of our itmes (lst) using lister. */
<a name="747"></a>        showSimpleList(lister, lst);
<a name="748"></a>        
<a name="749"></a>        /* Display the closing parenthesis. */
<a name="750"></a>        ")";
<a name="751"></a>    }
<a name="752"></a>;
<a name="753"></a>
<a name="754"></a>/* A ListGroupCuatom displays the list or a summary of the list in a user-defined way, */
<a name="755"></a>
<a name="756"></a>class ListGroupCustom: ListGroup
<a name="757"></a>    
<a name="758"></a>    /* The text to display to define this group. Tbis must be defined in user code. */
<a name="759"></a>    showGroupMsg(lst) { }
<a name="760"></a>    
<a name="761"></a>    /* Show our list. */
<a name="762"></a>    showList(lister, lst)
<a name="763"></a>    {
<a name="764"></a>        /* Call showGroupMsg(lst) to do the work of displaying it. */
<a name="765"></a>        showGroupMsg(lst);
<a name="766"></a>    }
<a name="767"></a>;
</pre>
<div class=ftr>Adv3Lite Library Reference Manual<br>Generated on 03/03/2025 from adv3Lite version 2.2.0.3</div>
</body>
</html>
