<html><head><link rel=stylesheet type="text/css" href="../libref.css"><title>precond.t</title></head><body>
<table class=ban><tr><td><h1>precond.t</h1><td align=right><a href="../file/precond.t.html">documentation</a></table><pre>
<a name="1"></a>#charset "us-ascii"
<a name="2"></a>#include "advlite.h"
<a name="3"></a>
<a name="4"></a>
<a name="5"></a>/*
<a name="6"></a> *   ***************************************************************************
<a name="7"></a> *   precond.t
<a name="8"></a> *
<a name="9"></a> *   This module forms part of the adv3Lite library (c) 2012-13 Eric Eve
<a name="10"></a> *
<a name="11"></a> *   Based on code in the adv3 library (c) Michael J. Roberts.
<a name="12"></a> */
<a name="13"></a>
<a name="14"></a>/* 
<a name="15"></a> *   A PreCondition encapsulate a condition that must be fulfilled in order for
<a name="16"></a> *   an action to be fulfilled (e.g. a container must be open before we can put
<a name="17"></a> *   anything in it). A PreCondition may also try to bring about the fulfilment
<a name="18"></a> *   of the condition it enforces via an implicit action.
<a name="19"></a> */
<a name="20"></a>class PreCondition: object
<a name="21"></a>    /*
<a name="22"></a>     *   Check the condition on the given object (which may be nil, if
<a name="23"></a>     *   this condition doesn't apply specifically to one of the objects
<a name="24"></a>     *   in the command).  If it is possible to meet the condition with an
<a name="25"></a>     *   implicit command, and allowImplicit is true, try to execute the
<a name="26"></a>     *   command.  If the condition cannot be met, report a failure and
<a name="27"></a>     *   return nil to terminate the command.
<a name="28"></a>     *   
<a name="29"></a>     *   If allowImplicit is nil, an implicit command may not be
<a name="30"></a>     *   attempted.  In this case, if the condition is not met, we must
<a name="31"></a>     *   simply report a failure return nil to terminate the command.
<a name="32"></a>     */
<a name="33"></a>    checkPreCondition(obj, allowImplicit) { return true; }
<a name="34"></a>
<a name="35"></a>    /*
<a name="36"></a>     *   Verify the condition.  This is called during the object
<a name="37"></a>     *   verification step so that the pre-condition can add verifications
<a name="38"></a>     *   of its own.  This can be used, for example, to add likelihood to
<a name="39"></a>     *   objects that already meet the condition.  Note that it is
<a name="40"></a>     *   generally not desirable to report illogical for conditions that
<a name="41"></a>     *   checkPreCondition() enforces, because doing so will prevent
<a name="42"></a>     *   checkPreCondition() from ever being reached and thus will prevent
<a name="43"></a>     *   checkPreCondition() from attempting to carry out implicit actions
<a name="44"></a>     *   to meet the condition.
<a name="45"></a>     *   
<a name="46"></a>     *   'obj' is the object being checked.  Note that because this is
<a name="47"></a>     *   called during verification, the explicitly passed-in object must
<a name="48"></a>     *   be used in the check rather than the current object in the global
<a name="49"></a>     *   current action.  
<a name="50"></a>     */
<a name="51"></a>    verifyPreCondition(obj) { }
<a name="52"></a>
<a name="53"></a>    /*
<a name="54"></a>     *   Precondition execution order.  When we execute preconditions for a
<a name="55"></a>     *   given action, we'll sort the list of all applicable preconditions
<a name="56"></a>     *   in ascending execution order.
<a name="57"></a>     *   
<a name="58"></a>     *   For the most part, the relative order of two preconditions is
<a name="59"></a>     *   arbitrary.  In some unusual cases, though, the order is important,
<a name="60"></a>     *   such as when applying one precondition can destroy the conditions
<a name="61"></a>     *   that the other would try to create but not vice versa.  When the
<a name="62"></a>     *   order doesn't matter, this can be left at the default setting.  
<a name="63"></a>     */
<a name="64"></a>    preCondOrder = 100
<a name="65"></a>;
<a name="66"></a>    
<a name="67"></a>/* 
<a name="68"></a> *   A precondition to check whether a container is open. If this is defined on
<a name="69"></a> *   the action handling of an object, it only takes effect if that object
<a name="70"></a> *   behaves like a container, in which case either it, or its remapIn object,
<a name="71"></a> *   must be open for the action to proceed.
<a name="72"></a> */
<a name="73"></a>containerOpen: PreCondition
<a name="74"></a>    checkPreCondition(obj, allowImplicit) 
<a name="75"></a>    { 
<a name="76"></a>        /* 
<a name="77"></a>         *   if we have a non-nil remapIn property, that's the container
<a name="78"></a>         *   representing us, so we need to use it instead.
<a name="79"></a>         */        
<a name="80"></a>        if(obj.remapIn != nil)
<a name="81"></a>            obj = obj.remapIn;
<a name="82"></a>        
<a name="83"></a>        
<a name="84"></a>        /* 
<a name="85"></a>         *   If the object is already open, we're already done; also there's
<a name="86"></a>         *   nothing to be done if we're not a container (in the sense of
<a name="87"></a>         *   something objects can be put in).
<a name="88"></a>         */               
<a name="89"></a>        if (obj == nil || obj.contType != In || obj.isOpen)
<a name="90"></a>            return true;
<a name="91"></a>        
<a name="92"></a>        /* If we're allowed to try an implicit action, then try opening obj */
<a name="93"></a>        if(allowImplicit) 
<a name="94"></a>        {
<a name="95"></a>            /* 
<a name="96"></a>             *   Try opening obj via an implicit action and note whether tha
<a name="97"></a>             *   action was actually attempted.
<a name="98"></a>             */                
<a name="99"></a>            local tried = tryImplicitAction(Open, obj);
<a name="100"></a>            
<a name="101"></a>            /* 
<a name="102"></a>             *   If obj is now open, this precondition has been met, so return
<a name="103"></a>             *   true.
<a name="104"></a>             */
<a name="105"></a>            if(obj.isOpen)
<a name="106"></a>                return true;
<a name="107"></a>            
<a name="108"></a>            /* 
<a name="109"></a>             *   Otherwise if we tried but failed to open obj, return nil to
<a name="110"></a>             *   signal that we haven't met this precondition so the main action
<a name="111"></a>             *   can't go ahead. The attempt to open obj will have reported the
<a name="112"></a>             *   reason why it failed, so there's no need to display anything
<a name="113"></a>             *   else.
<a name="114"></a>             */
<a name="115"></a>            if(tried)
<a name="116"></a>                return nil;
<a name="117"></a>        }
<a name="118"></a>        
<a name="119"></a>        /* 
<a name="120"></a>         *   If we reach here obj is closed and we weren't allowed to try to
<a name="121"></a>         *   open it; display a message explaining the problem.
<a name="122"></a>         */
<a name="123"></a>        gMessageParams(obj);
<a name="124"></a>        DMsg(container needs to be open, '{The subj obj} need{s/ed} to be open
<a name="125"></a>            for that. ');
<a name="126"></a>        
<a name="127"></a>        /* Then return nil to indicate that the precondition hasn't been met. */
<a name="128"></a>        return nil;        
<a name="129"></a>    }
<a name="130"></a>    
<a name="131"></a>;
<a name="132"></a>
<a name="133"></a>/* A PreCondition to check whether an object is open. */
<a name="134"></a>objOpen: PreCondition
<a name="135"></a>    
<a name="136"></a>    verifyPreCondition(obj)
<a name="137"></a>    {
<a name="138"></a>        /* 
<a name="139"></a>         *   If the object isn't open already, make it a slightly less logical
<a name="140"></a>         *   choice of object for this command.
<a name="141"></a>         */
<a name="142"></a>        if(!obj.isOpen)
<a name="143"></a>            logicalRank(90);
<a name="144"></a>    }
<a name="145"></a>    
<a name="146"></a>    checkPreCondition(obj, allowImplicit) 
<a name="147"></a>    { 
<a name="148"></a>        /* 
<a name="149"></a>         *   if the object is already open, we're already done.
<a name="150"></a>         */
<a name="151"></a>        if (obj == nil || obj.isOpen)
<a name="152"></a>            return true;
<a name="153"></a>        
<a name="154"></a>        /* 
<a name="155"></a>         *   If we're allowed to attempt an implicit action, try opening obj
<a name="156"></a>         *   implicitly and see if we succeed.
<a name="157"></a>         */
<a name="158"></a>        if(allowImplicit) 
<a name="159"></a>        {
<a name="160"></a>            /* 
<a name="161"></a>             *   Try opening obj implicitly and note if we were allowed to make
<a name="162"></a>             *   the attempt.
<a name="163"></a>             */
<a name="164"></a>            local tried = tryImplicitAction(Open, obj);
<a name="165"></a>            
<a name="166"></a>            /*  
<a name="167"></a>             *   If obj is now open return true to signal that this precondition
<a name="168"></a>             *   has now been met.
<a name="169"></a>             */
<a name="170"></a>            if(obj.isOpen)
<a name="171"></a>                return true;
<a name="172"></a>            
<a name="173"></a>            /* 
<a name="174"></a>             *   Otherwise, if we tried but failed to open obj, return nil to
<a name="175"></a>             *   signal that this precondition can't be met (so the main action
<a name="176"></a>             *   cannot proceed). The attempt to open obj will have explained
<a name="177"></a>             *   why it failed, so there's no need for any further explanation
<a name="178"></a>             *   here.
<a name="179"></a>             */
<a name="180"></a>            if(tried)
<a name="181"></a>                return nil;
<a name="182"></a>        }
<a name="183"></a>        
<a name="184"></a>        /* 
<a name="185"></a>         *   If we reach here obj is closed and we weren't allowed to try to
<a name="186"></a>         *   open it; display a message explaining the problem.
<a name="187"></a>         */
<a name="188"></a>        gMessageParams(obj);
<a name="189"></a>        DMsg(object needs to be open, '{The subj obj} need{s/ed} to be open for
<a name="190"></a>            that. ');
<a name="191"></a>        
<a name="192"></a>        /* Then return nil to indicate that the precondition hasn't been met. */
<a name="193"></a>        return nil;        
<a name="194"></a>    }
<a name="195"></a>    
<a name="196"></a>;
<a name="197"></a>
<a name="198"></a>
<a name="199"></a>/* A PreCondition to check whether an object is closed. */
<a name="200"></a>objClosed: PreCondition
<a name="201"></a>    
<a name="202"></a>    verifyPreCondition(obj)
<a name="203"></a>    {
<a name="204"></a>        /* 
<a name="205"></a>         *   If the object is open, make it a slightly less logical choice of
<a name="206"></a>         *   object for this command.
<a name="207"></a>         */
<a name="208"></a>        if(obj.isOpen)
<a name="209"></a>            logicalRank(90);
<a name="210"></a>    }
<a name="211"></a>    
<a name="212"></a>    
<a name="213"></a>    checkPreCondition(obj, allowImplicit) 
<a name="214"></a>    { 
<a name="215"></a>        /* 
<a name="216"></a>         *   if the object is already closed, we're already done.
<a name="217"></a>         */
<a name="218"></a>        if (obj == nil || ! obj.isOpen)
<a name="219"></a>            return true;
<a name="220"></a>        
<a name="221"></a>        
<a name="222"></a>        /* 
<a name="223"></a>         *   If we're allowed to attempt an implicit action, try closing obj
<a name="224"></a>         *   implicitly and see if we succeed.
<a name="225"></a>         */
<a name="226"></a>        if(allowImplicit) 
<a name="227"></a>        {
<a name="228"></a>            /* 
<a name="229"></a>             *   Try closing obj implicitly and note if we were allowed to make
<a name="230"></a>             *   the attempt.
<a name="231"></a>             */
<a name="232"></a>            local tried = tryImplicitAction(Close, obj);
<a name="233"></a>            
<a name="234"></a>             /*  
<a name="235"></a>              *   If obj is now closed return true to signal that this
<a name="236"></a>              *   precondition has now been met.
<a name="237"></a>              */            
<a name="238"></a>            if(!obj.isOpen)
<a name="239"></a>                return true;
<a name="240"></a>            
<a name="241"></a>            /* 
<a name="242"></a>             *   Otherwise, if we tried but failed to open obj, return nil to
<a name="243"></a>             *   signal that this precondition can't be met (so the main action
<a name="244"></a>             *   cannot proceed). The attempt to close obj will have explained
<a name="245"></a>             *   why it failed, so there's no need for any further explanation
<a name="246"></a>             *   here.
<a name="247"></a>             */
<a name="248"></a>            if(tried)
<a name="249"></a>                return nil;
<a name="250"></a>        }
<a name="251"></a>        
<a name="252"></a>        
<a name="253"></a>        /* 
<a name="254"></a>         *   If we reach here obj is open and we weren't allowed to try to
<a name="255"></a>         *   close it; display a message explaining the problem.
<a name="256"></a>         */
<a name="257"></a>        gMessageParams(obj);        
<a name="258"></a>        DMsg(obj needs to be closed, '{The subj obj} need{s/ed} to be closed for
<a name="259"></a>            that. ');
<a name="260"></a>        
<a name="261"></a>        /* Then return nil to indicate that the precondition hasn't been met. */
<a name="262"></a>        return nil;        
<a name="263"></a>    }
<a name="264"></a>    
<a name="265"></a>;  
<a name="266"></a>    
<a name="267"></a>/* 
<a name="268"></a> *   A PreCondition to check whether an object is unlocked, and to attempt to
<a name="269"></a> *   unlock it if it seems possible to do so.
<a name="270"></a> */
<a name="271"></a>objUnlocked: PreCondition
<a name="272"></a>    checkPreCondition(obj, allowImplicit) 
<a name="273"></a>    { 
<a name="274"></a>        /* 
<a name="275"></a>         *   If the object is already unlocked, we're already done. If the
<a name="276"></a>         *   object is neither lockableWithKey nor lockableWithoutKey there's
<a name="277"></a>         *   nothing we can do here.
<a name="278"></a>         */
<a name="279"></a>        if (obj == nil || ! obj.isLocked 
<a name="280"></a>            || obj.lockability not in (lockableWithoutKey, lockableWithKey) )
<a name="281"></a>            return true;
<a name="282"></a>        
<a name="283"></a>        /* 
<a name="284"></a>         *   If the object can be unlocked without a Key, try unlocking it
<a name="285"></a>         *   implicitly
<a name="286"></a>         */
<a name="287"></a>        if(allowImplicit &amp;&amp; obj.lockability == lockableWithoutKey)
<a name="288"></a>        {
<a name="289"></a>            /* 
<a name="290"></a>             *   Try unlocking obj implicitly and note if we were allowed to make
<a name="291"></a>             *   the attempt.
<a name="292"></a>             */
<a name="293"></a>            local tried = tryImplicitAction(Unlock, obj);
<a name="294"></a>            
<a name="295"></a>             /*  
<a name="296"></a>              *   If obj is now unlocked return true to signal that this
<a name="297"></a>              *   precondition has now been met.
<a name="298"></a>              */            
<a name="299"></a>            if(!obj.isLocked)
<a name="300"></a>                return true;
<a name="301"></a>            
<a name="302"></a>            /* 
<a name="303"></a>             *   Otherwise, if we tried but failed to unlock obj, return nil to
<a name="304"></a>             *   signal that this precondition can't be met (so the main action
<a name="305"></a>             *   cannot proceed). The attempt to unlock obj will have explained
<a name="306"></a>             *   why it failed, so there's no need for any further explanation
<a name="307"></a>             *   here.
<a name="308"></a>             */
<a name="309"></a>            if(tried)
<a name="310"></a>                return nil;
<a name="311"></a>        }
<a name="312"></a>        
<a name="313"></a>        
<a name="314"></a>        /* 
<a name="315"></a>         *   If the object needs a key to unlock it, attempt to unlock it with a
<a name="316"></a>         *   plausible key if there is one, otherwise return true to let the
<a name="317"></a>         *   OPEN action go ahead and fail.
<a name="318"></a>         */
<a name="319"></a>        
<a name="320"></a>        if(allowImplicit &amp;&amp; obj.lockability == lockableWithKey)
<a name="321"></a>        {
<a name="322"></a>            /* 
<a name="323"></a>             *   Try to find a key held by the actor that may unlock this
<a name="324"></a>             *   object; the result will be stored in the useKey_ property of
<a name="325"></a>             *   the object.
<a name="326"></a>             */
<a name="327"></a>            obj.findPlausibleKey(true);
<a name="328"></a>            
<a name="329"></a>            /* 
<a name="330"></a>             *   If we don't find a key, return TRUE to let the OPEN action go
<a name="331"></a>             *   ahead and report that the object is locked, since there's
<a name="332"></a>             *   nothing else we can do here.
<a name="333"></a>             */
<a name="334"></a>            if(obj.useKey_ == nil)
<a name="335"></a>                return true;
<a name="336"></a>            
<a name="337"></a>            /* 
<a name="338"></a>             *   Try unlocking obj with useKey_ implicitly and note if we were
<a name="339"></a>             *   allowed to make the attempt.
<a name="340"></a>             */
<a name="341"></a>            local tried = tryImplicitAction(UnlockWith, obj, obj.useKey_);
<a name="342"></a>            
<a name="343"></a>             /*  
<a name="344"></a>              *   If obj is now unlocked return true to signal that this
<a name="345"></a>              *   precondition has now been met.
<a name="346"></a>              */            
<a name="347"></a>            if(!obj.isLocked)
<a name="348"></a>                return true;
<a name="349"></a>            
<a name="350"></a>            /* 
<a name="351"></a>             *   Otherwise, if we tried but failed to unlock obj, return nil to
<a name="352"></a>             *   signal that this precondition can't be met (so the main action
<a name="353"></a>             *   cannot proceed). The attempt to unlock obj will have explained
<a name="354"></a>             *   why it failed, so there's no need for any further explanation
<a name="355"></a>             *   here.
<a name="356"></a>             */
<a name="357"></a>            if(tried)
<a name="358"></a>                return nil;
<a name="359"></a>        }
<a name="360"></a>        
<a name="361"></a>        /* 
<a name="362"></a>         *   Although the PreCondition hasn't been met, we'll return true at
<a name="363"></a>         *   this point to let the OPEN action go ahead and report the problem.
<a name="364"></a>         */
<a name="365"></a>        
<a name="366"></a>        return true;
<a name="367"></a>    }
<a name="368"></a>;
<a name="369"></a>
<a name="370"></a>    
<a name="371"></a>/* A PreCondition to check whether an object is currently held by the actor. */
<a name="372"></a>objHeld: PreCondition
<a name="373"></a>    
<a name="374"></a>    verifyPreCondition(obj)
<a name="375"></a>    {
<a name="376"></a>        /* 
<a name="377"></a>         *   If the object is fixed in place it can't be picked up, so there's
<a name="378"></a>         *   no point in trying. BUT we also have to check for the case that the
<a name="379"></a>         *   object is directly in the player (perhaps because a body part).
<a name="380"></a>         */        
<a name="381"></a>        if(obj.isFixed &amp;&amp; !obj.isDirectlyIn(gActor))
<a name="382"></a>            illogical(obj.cannotTakeMsg);
<a name="383"></a>        
<a name="384"></a>        
<a name="385"></a>        /* 
<a name="386"></a>         *   If the actor isn't carrying the object it's slightly less likely to
<a name="387"></a>         *   be the one the player means.
<a name="388"></a>         */        
<a name="389"></a>        if(!obj.isIn(gActor))
<a name="390"></a>            logicalRank(90);
<a name="391"></a>        
<a name="392"></a>    }
<a name="393"></a>    
<a name="394"></a>    checkPreCondition(obj, allowImplicit) 
<a name="395"></a>    { 
<a name="396"></a>        /* 
<a name="397"></a>         *   if the object is already held, we're already done.
<a name="398"></a>         */
<a name="399"></a>        if (obj == nil || obj.isDirectlyIn(gActor))
<a name="400"></a>            return true;
<a name="401"></a>        
<a name="402"></a>        
<a name="403"></a>        /* 
<a name="404"></a>         *   If we're allowed to attempt an implicit action, try taking obj
<a name="405"></a>         *   implicitly and see if we succeed.
<a name="406"></a>         */
<a name="407"></a>        if(allowImplicit) 
<a name="408"></a>        {    
<a name="409"></a>            /* 
<a name="410"></a>             *   Try taking obj implicitly and note if we were allowed to make
<a name="411"></a>             *   the attempt.
<a name="412"></a>             */
<a name="413"></a>            local tried = tryImplicitAction(Take, obj);
<a name="414"></a>            
<a name="415"></a>            /*  
<a name="416"></a>             *   If obj is now held by the actor return true to signal that this
<a name="417"></a>             *   precondition has now been met.
<a name="418"></a>             */
<a name="419"></a>            if(obj.isDirectlyIn(gActor))
<a name="420"></a>              return true;   
<a name="421"></a>            
<a name="422"></a>            /* 
<a name="423"></a>             *   Otherwise, if we tried but failed to take obj, return nil to
<a name="424"></a>             *   signal that this precondition can't be met (so the main action
<a name="425"></a>             *   cannot proceed). The attempt to take obj will have explained
<a name="426"></a>             *   why it failed, so there's no need for any further explanation
<a name="427"></a>             *   here.
<a name="428"></a>             */
<a name="429"></a>            if(tried)
<a name="430"></a>                return nil;            
<a name="431"></a>        }
<a name="432"></a>        
<a name="433"></a>        /* 
<a name="434"></a>         *   If we reach here obj isn't being held by the actor and we weren't
<a name="435"></a>         *   allowed to try to take it; display a message explaining the
<a name="436"></a>         *   problem.
<a name="437"></a>         */
<a name="438"></a>        gMessageParams(obj);
<a name="439"></a>        DMsg(need to hold, '{I} need{s/ed} to be holding {the obj} to do that. ');
<a name="440"></a>        
<a name="441"></a>        /* Then return nil to indicate that the precondition hasn't been met. */
<a name="442"></a>        return nil;        
<a name="443"></a>    }
<a name="444"></a>;
<a name="445"></a>
<a name="446"></a>/* A PreCondition to check that an object isn't being worn. */
<a name="447"></a>objNotWorn: PreCondition
<a name="448"></a>    checkPreCondition(obj, allowImplicit) 
<a name="449"></a>    { 
<a name="450"></a>        /* 
<a name="451"></a>         *   if the object is not being worn, we're already done.
<a name="452"></a>         */
<a name="453"></a>        if (obj == nil || obj.wornBy != gActor)
<a name="454"></a>            return true;
<a name="455"></a>        
<a name="456"></a>        /* 
<a name="457"></a>         *   If we're allowed to attempt an implicit action, try taking off obj
<a name="458"></a>         *   implicitly and see if we succeed.
<a name="459"></a>         */        
<a name="460"></a>        if(allowImplicit) 
<a name="461"></a>        {
<a name="462"></a>            /* 
<a name="463"></a>             *   Try taking off obj implicitly and note if we were allowed to
<a name="464"></a>             *   make the attempt.
<a name="465"></a>             */
<a name="466"></a>            local tried = tryImplicitAction(Doff, obj);
<a name="467"></a>            
<a name="468"></a>            /*  
<a name="469"></a>             *   If obj is now not being worn return true to signal that this
<a name="470"></a>             *   precondition has now been met.
<a name="471"></a>             */
<a name="472"></a>            if(obj.wornBy == nil)
<a name="473"></a>                return true;
<a name="474"></a>            
<a name="475"></a>            /* 
<a name="476"></a>             *   Otherwise, if we tried but failed to take obj off, return nil
<a name="477"></a>             *   to signal that this precondition can't be met (so the main
<a name="478"></a>             *   action cannot proceed). The attempt to take off obj will have
<a name="479"></a>             *   explained why it failed, so there's no need for any further
<a name="480"></a>             *   explanation here.
<a name="481"></a>             */
<a name="482"></a>            if(tried)
<a name="483"></a>                return nil;
<a name="484"></a>            
<a name="485"></a>        }
<a name="486"></a>        
<a name="487"></a>        /* 
<a name="488"></a>         *   If we reach here obj is still being worn and we weren't allowed to
<a name="489"></a>         *   try to take it off; display a message explaining the problem.
<a name="490"></a>         */
<a name="491"></a>        gMessageParams(obj);
<a name="492"></a>        DMsg(cannot do that while wearing, '{I} {can\'t} do that while
<a name="493"></a>            {he actor}{\'m} wearing {the obj}. ');
<a name="494"></a>        
<a name="495"></a>        /* Then return nil to indicate that the precondition hasn't been met. */
<a name="496"></a>        return nil;        
<a name="497"></a>    }
<a name="498"></a>    
<a name="499"></a>;
<a name="500"></a>
<a name="501"></a>/* A PreCondition to check that an object is visible. */
<a name="502"></a>objVisible: PreCondition
<a name="503"></a>    verifyPreCondition(obj)
<a name="504"></a>    {
<a name="505"></a>        /* If it's too dark to see then we can't examine the object */        
<a name="506"></a>        if(!(gActor.outermostVisibleParent().isIlluminated || obj.visibleInDark))
<a name="507"></a>            inaccessible(obj.tooDarkToSeeMsg);
<a name="508"></a>        
<a name="509"></a>        /* If the actor can't see the obj then we can't examine the object. */
<a name="510"></a>        if(!Q.canSee(gActor, obj))
<a name="511"></a>            inaccessible(BMsg(cannot see obj, '{I} {can\'t} see {1}. ',
<a name="512"></a>                         obj.theName));
<a name="513"></a>    }   
<a name="514"></a>;
<a name="515"></a>
<a name="516"></a>/* A PreCondition to check that an object is audible. */
<a name="517"></a>objAudible: PreCondition
<a name="518"></a>    verifyPreCondition(obj)
<a name="519"></a>    {
<a name="520"></a>       /* 
<a name="521"></a>        *   Construct a list of objects that are blocking the sound path between
<a name="522"></a>        *   the actor and obj.
<a name="523"></a>        */
<a name="524"></a>        local lst = Q.soundBlocker(gActor, obj);
<a name="525"></a>        
<a name="526"></a>        /* 
<a name="527"></a>         *   If the actor cannot hear obj and there's at least one object
<a name="528"></a>         *   blocking the sound path between them, construct an appropriate
<a name="529"></a>         *   error message and block the action.
<a name="530"></a>         */      
<a name="531"></a>        if(!Q.canHear(gActor, obj) &amp;&amp; lst.length &gt; 0)
<a name="532"></a>        {            
<a name="533"></a>            local errMsg;
<a name="534"></a>            gMessageParams(obj);
<a name="535"></a>         
<a name="536"></a>            /* 
<a name="537"></a>             *   If the blocking object is a Room, then obj is in a remote
<a name="538"></a>             *   location, so the reason the actor can't hear it is that it's
<a name="539"></a>             *   too far away.
<a name="540"></a>             */
<a name="541"></a>            if(lst[1].ofKind(Room))
<a name="542"></a>                errMsg = BMsg(too far away to hear obj, '{The subj obj} {is} too
<a name="543"></a>                    far away to hear. ');
<a name="544"></a>            
<a name="545"></a>            /* 
<a name="546"></a>             *   Otherwise the reason the actor can't hear obj is that the first
<a name="547"></a>             *   blocking object is in the way.
<a name="548"></a>             */
<a name="549"></a>            else           
<a name="550"></a>                errMsg = BMsg(cannot hear, '{I} {can\'t} hear {1} 
<a name="551"></a>                through {2}. ', obj.theName, lst[1].theName);
<a name="552"></a>                
<a name="553"></a>            /* Declare obj to be inaccessible to hearing. */
<a name="554"></a>            inaccessible(errMsg);
<a name="555"></a>        }        
<a name="556"></a>    }
<a name="557"></a>;
<a name="558"></a>
<a name="559"></a>/* A PreCondition to check that an object is smellable. */
<a name="560"></a>objSmellable: PreCondition
<a name="561"></a>    verifyPreCondition(obj)
<a name="562"></a>    {
<a name="563"></a>       /* 
<a name="564"></a>        *   Construct a list of objects that are blocking the scent path between
<a name="565"></a>        *   the actor and obj.
<a name="566"></a>        */
<a name="567"></a>        local lst = Q.scentBlocker(gActor, obj);
<a name="568"></a>        
<a name="569"></a>        /* 
<a name="570"></a>         *   If the actor cannot hear obj and there's at least one object
<a name="571"></a>         *   blocking the sound path between them, construct an appropriate
<a name="572"></a>         *   error message and block the action.
<a name="573"></a>         */      
<a name="574"></a>        if(!Q.canSmell(gActor, obj) &amp;&amp; lst.length &gt; 0)
<a name="575"></a>        {            
<a name="576"></a>            local errMsg;
<a name="577"></a>            gMessageParams(obj);
<a name="578"></a>         
<a name="579"></a>            /* 
<a name="580"></a>             *   If the blocking object is a Room, then obj is in a remote
<a name="581"></a>             *   location, so the reason the actor can't smell it is that it's
<a name="582"></a>             *   too far away.
<a name="583"></a>             */
<a name="584"></a>            if(lst[1].ofKind(Room))
<a name="585"></a>                errMsg = BMsg(too far away to smell obj, '{The subj obj} {is}
<a name="586"></a>                    too far away to smell. ');
<a name="587"></a>            
<a name="588"></a>            /* 
<a name="589"></a>             *   Otherwise the reason the actor can't smell obj is that the first
<a name="590"></a>             *   blocking object is in the way.
<a name="591"></a>             */
<a name="592"></a>            else           
<a name="593"></a>                errMsg = BMsg(cannot smell through, '{I} {can\'t} smell {1} 
<a name="594"></a>                through {2}. ', obj.theName, lst[1].theName);
<a name="595"></a>                
<a name="596"></a>            /* Declare obj to be inaccessible to hearing. */
<a name="597"></a>            inaccessible(errMsg);
<a name="598"></a>        }        
<a name="599"></a>    }
<a name="600"></a>;
<a name="601"></a>
<a name="602"></a>/* 
<a name="603"></a> *   A PreCondition to check that an object can be touched (which is likely to
<a name="604"></a> *   be needed for any action that manipulated the object). This Precondition
<a name="605"></a> *   farms out as much of the detailed checking as possible to the Query object. 
<a name="606"></a> */
<a name="607"></a>touchObj: PreCondition    
<a name="608"></a>    verifyPreCondition(obj)
<a name="609"></a>    {
<a name="610"></a>        /* 
<a name="611"></a>         *   Store any issues that the Query object finds with reaching obj from
<a name="612"></a>         *   gActor
<a name="613"></a>         */
<a name="614"></a>        local reachIssues = Q.reachProblemVerify(gActor, obj);
<a name="615"></a>        
<a name="616"></a>        /*  Run the verify method of any issues we found */
<a name="617"></a>        foreach(local issue in reachIssues)        
<a name="618"></a>            issue.verify();        
<a name="619"></a>    }
<a name="620"></a>    
<a name="621"></a>    
<a name="622"></a>    
<a name="623"></a>    checkPreCondition(obj, allowImplicit)
<a name="624"></a>    {
<a name="625"></a>        /* 
<a name="626"></a>         *   Obtain any issues the Query object finds with reaching obj from
<a name="627"></a>         *   gActor at the check stage.
<a name="628"></a>         */
<a name="629"></a>        
<a name="630"></a>        local reachIssues = Q.reachProblemCheck(gActor, obj);
<a name="631"></a>        
<a name="632"></a>        /* 
<a name="633"></a>         *   Go through each stored issue in turn running its check method; if
<a name="634"></a>         *   any of the check methods return nil, exit and return nil.
<a name="635"></a>         */        
<a name="636"></a>        foreach(local issue in reachIssues)
<a name="637"></a>        {
<a name="638"></a>            if(issue.check(allowImplicit) == nil)
<a name="639"></a>                return nil;
<a name="640"></a>        }
<a name="641"></a>               
<a name="642"></a>        /* 
<a name="643"></a>         *   If we reach here we've passed all the checks, so return true to
<a name="644"></a>         *   indicate success.
<a name="645"></a>         */
<a name="646"></a>        return true;
<a name="647"></a>    }
<a name="648"></a>    
<a name="649"></a>    
<a name="650"></a>    preCondOrder = 80    
<a name="651"></a>;
<a name="652"></a>
<a name="653"></a>/* Declare attachedTo as a property since the attachables module is optional. */
<a name="654"></a>property attachedTo;
<a name="655"></a>property attachedToList;
<a name="656"></a>
<a name="657"></a>
<a name="658"></a>/* A PreCondition to check that an object isn't attached to anything. */
<a name="659"></a>objDetached: PreCondition
<a name="660"></a>    verifyPreCondition(obj)
<a name="661"></a>    {
<a name="662"></a>        /* 
<a name="663"></a>         *   An object that is attached may be a slightly less logical choice
<a name="664"></a>         *   for this action.
<a name="665"></a>         */
<a name="666"></a>        if(obj.attachedTo != nil)
<a name="667"></a>            logicalRank(90);
<a name="668"></a>    }
<a name="669"></a> 
<a name="670"></a>    checkPreCondition(obj, allowImplicit)
<a name="671"></a>    {
<a name="672"></a>        /* If the object isn't attached, we're done */
<a name="673"></a>        if(obj.attachedToList.length == 0)
<a name="674"></a>            return true;
<a name="675"></a>        
<a name="676"></a>         /* 
<a name="677"></a>          *   If we're allowed to attempt an implicit action, try detaching obj
<a name="678"></a>          *   implicitly and see if we succeed.
<a name="679"></a>          */
<a name="680"></a>        if(allowImplicit)            
<a name="681"></a>        {
<a name="682"></a>            /* 
<a name="683"></a>             *   Try detaching obj implicitly and note if we were allowed to
<a name="684"></a>             *   make the attempt.
<a name="685"></a>             */
<a name="686"></a>            local tried = nil;
<a name="687"></a>                
<a name="688"></a>            foreach(local cur in obj.attachedToList)
<a name="689"></a>                tried = tryImplicitAction(DetachFrom, obj, cur);
<a name="690"></a>            
<a name="691"></a>            /*  
<a name="692"></a>             *   If obj is now not attached to anything return true to signal
<a name="693"></a>             *   that this precondition has now been met.
<a name="694"></a>             */
<a name="695"></a>            if(obj.attachedToList.length == 0)
<a name="696"></a>               return true;
<a name="697"></a>            
<a name="698"></a>            /* 
<a name="699"></a>             *   Otherwise, if we tried but failed to detach obj, return nil to
<a name="700"></a>             *   signal that this precondition can't be met (so the main action
<a name="701"></a>             *   cannot proceed). The attempt to detach obj will have explained
<a name="702"></a>             *   why it failed, so there's no need for any further explanation
<a name="703"></a>             *   here.
<a name="704"></a>             */
<a name="705"></a>            if(tried)
<a name="706"></a>                return nil;
<a name="707"></a>        }
<a name="708"></a>        
<a name="709"></a>        /* 
<a name="710"></a>         *   If we reach here obj is still attached to something and we weren't
<a name="711"></a>         *   allowed to try to detach it; display a message explaining the
<a name="712"></a>         *   problem.
<a name="713"></a>         */
<a name="714"></a>        local att = obj.attachedTo;
<a name="715"></a>        gMessageParams(obj, att);
<a name="716"></a>        
<a name="717"></a>        DMsg(cannot do that while attached, '{I} {can\'t} do that while {the subj
<a name="718"></a>            obj} is attached to {the att}. ');
<a name="719"></a>        
<a name="720"></a>        /* Then return nil to indicate that the precondition hasn't been met. */
<a name="721"></a>        return nil;  
<a name="722"></a>    }
<a name="723"></a>;
<a name="724"></a>
<a name="725"></a>
<a name="726"></a>/* ------------------------------------------------------------------------ */
<a name="727"></a>/*
<a name="728"></a> *   A pre-condition that applies to a specific, pre-determined object,
<a name="729"></a> *   rather than the direct/indirect object of the command.
<a name="730"></a> */
<a name="731"></a>class ObjectPreCondition: PreCondition
<a name="732"></a>    construct(obj, cond)
<a name="733"></a>    {
<a name="734"></a>        /* 
<a name="735"></a>         *   remember the specific object I act upon, and the underlying
<a name="736"></a>         *   precondition to apply to that object 
<a name="737"></a>         */
<a name="738"></a>        obj_ = obj;
<a name="739"></a>        cond_ = cond;
<a name="740"></a>    }
<a name="741"></a>
<a name="742"></a>    /* route our check to the pre-condition using our specific object */
<a name="743"></a>    checkPreCondition(obj, allowImplicit)
<a name="744"></a>    {
<a name="745"></a>        /* check the precondition */
<a name="746"></a>        return cond_.checkPreCondition(obj_, allowImplicit);
<a name="747"></a>    }
<a name="748"></a>
<a name="749"></a>    /* route our verification check to the pre-condition */
<a name="750"></a>    verifyPreCondition(obj)
<a name="751"></a>    {
<a name="752"></a>        cond_.verifyPreCondition(obj_);
<a name="753"></a>    }
<a name="754"></a>
<a name="755"></a>    /* use the same order as our underlying condition */
<a name="756"></a>    preCondOrder = (cond_.preCondOrder)
<a name="757"></a>
<a name="758"></a>    /* the object we check with the condition */
<a name="759"></a>    obj_ = nil
<a name="760"></a>
<a name="761"></a>    /* the pre-condition we check */
<a name="762"></a>    cond_ = nil
<a name="763"></a>;
<a name="764"></a>
<a name="765"></a>/* -------------------------------------------------------------------------- */
<a name="766"></a>
<a name="767"></a>actorInStagingLocation: PreCondition
<a name="768"></a>    checkPreCondition(obj, allowImplicit)
<a name="769"></a>    {
<a name="770"></a>        local loc = gActor.location;
<a name="771"></a>        local stagingLoc = obj.stagingLocation;
<a name="772"></a>        local action;
<a name="773"></a>        
<a name="774"></a>        /* If the actor's location is the staging location then we're done. */
<a name="775"></a>        if(loc == stagingLoc)
<a name="776"></a>            return true;
<a name="777"></a>        
<a name="778"></a>        if(stagingLoc == nil)
<a name="779"></a>        {
<a name="780"></a>            gMessageParams(obj);
<a name="781"></a>            DMsg(no staging loc, '{The subj obj} {can\'t} be reached. ');
<a name="782"></a>            return nil;
<a name="783"></a>        }
<a name="784"></a>        
<a name="785"></a>        if(allowImplicit)
<a name="786"></a>        {
<a name="787"></a>            local tried = nil;
<a name="788"></a>            
<a name="789"></a>            while(!stagingLoc.isOrIsIn(loc))
<a name="790"></a>            {
<a name="791"></a>                action = loc.contType == In ? GetOutOf : GetOff;
<a name="792"></a>                tried = tryImplicitAction(action, loc);
<a name="793"></a>                if(gActor.location == loc)
<a name="794"></a>                    break;
<a name="795"></a>                
<a name="796"></a>                loc = gActor.location;
<a name="797"></a>            }
<a name="798"></a>                                  
<a name="799"></a>            if(stagingLoc == loc)
<a name="800"></a>                return true;
<a name="801"></a>            
<a name="802"></a>            local path = [];
<a name="803"></a>            local step = stagingLoc;
<a name="804"></a>            
<a name="805"></a>            while(step &amp;&amp; step != loc)
<a name="806"></a>            {
<a name="807"></a>                path = [step] + path;
<a name="808"></a>                step = step.stagingLocation;
<a name="809"></a>            }
<a name="810"></a>            
<a name="811"></a>            foreach(step in path)
<a name="812"></a>            {
<a name="813"></a>                action = step.contType == In ? Enter : Board;
<a name="814"></a>                tried = tryImplicitAction(action, step);                
<a name="815"></a>                if(gActor.location != step)
<a name="816"></a>                    break;
<a name="817"></a>            }
<a name="818"></a>            
<a name="819"></a>            if(stagingLoc == gActor.location)
<a name="820"></a>                return true;
<a name="821"></a>            
<a name="822"></a>            if(tried)
<a name="823"></a>                return nil;
<a name="824"></a>            
<a name="825"></a>            
<a name="826"></a>        }
<a name="827"></a>        
<a name="828"></a>        gMessageParams(stagingLoc);
<a name="829"></a>        
<a name="830"></a>        DMsg(not in staging location, '{I} need{s/ed} to be &lt;&lt;if
<a name="831"></a>              stagingLoc.ofKind(Room)&gt;&gt; directly &lt;&lt;end&gt;&gt;
<a name="832"></a>            {in stagingloc} to do that. ');
<a name="833"></a>    
<a name="834"></a>         /* Then return nil to indicate that the precondition hasn't been met. */
<a name="835"></a>        return nil;  
<a name="836"></a>        
<a name="837"></a>    }    
<a name="838"></a>;
<a name="839"></a>
<a name="840"></a>/* 
<a name="841"></a> *   A PreCondition to make sure that the actor is out of any nested rooms
<a name="842"></a> *   before the action takes place.
<a name="843"></a> */
<a name="844"></a>actorOutOfNested: PreCondition
<a name="845"></a>    checkPreCondition(obj, allowImplicit)
<a name="846"></a>    {
<a name="847"></a>        local loc = gActor.location;
<a name="848"></a>        local action;
<a name="849"></a>        
<a name="850"></a>        /* 
<a name="851"></a>         *   If the Actor's location is a room, then there's nothing to be done.
<a name="852"></a>         */
<a name="853"></a>        if(loc.ofKind(Room))
<a name="854"></a>            return true;
<a name="855"></a>        
<a name="856"></a>        if(allowImplicit)
<a name="857"></a>        {
<a name="858"></a>            local tried = nil;
<a name="859"></a>            while(!loc.ofKind(Room))
<a name="860"></a>            {
<a name="861"></a>                action = loc.contType == In ? GetOutOf : GetOff;
<a name="862"></a>                tried = tryImplicitAction(action, loc);
<a name="863"></a>                if(gActor.location == loc)
<a name="864"></a>                    break;
<a name="865"></a>                
<a name="866"></a>                loc = gActor.location;                
<a name="867"></a>            }
<a name="868"></a>            
<a name="869"></a>            /* If the actor is now in a Room, we've succeeded. */
<a name="870"></a>            if(loc.ofKind(Room))
<a name="871"></a>                return true;
<a name="872"></a>            
<a name="873"></a>            /* 
<a name="874"></a>             *   Otherwise, if we tried but failed to get out into the Room,
<a name="875"></a>             *   return nil to tell our caller we failed.
<a name="876"></a>             */
<a name="877"></a>            if(tried)
<a name="878"></a>                return nil;            
<a name="879"></a>        }
<a name="880"></a>        
<a name="881"></a>        gMessageParams(loc);
<a name="882"></a>        DMsg(still in nested, '{I} {can\'t} do that while {he actor} {is} 
<a name="883"></a>            {in loc}. ');
<a name="884"></a>        return nil;
<a name="885"></a>    }
<a name="886"></a>    
<a name="887"></a>;
<a name="888"></a>
<a name="889"></a>
<a name="890"></a>/*  
<a name="891"></a> *   PreCondition that calls before travel notifications (such as beforeTravel() on every object in
<a name="892"></a> *   scope) so these can rule out travel/push travel before it's attempted.
<a name="893"></a> */
<a name="894"></a>travelPermitted: PreCondition
<a name="895"></a>    checkPreCondition(obj, allowImplicit) 
<a name="896"></a>    { 
<a name="897"></a>      
<a name="898"></a>        obj.beforeTravelNotifications(gActor);
<a name="899"></a>        
<a name="900"></a>        if(gAction.isPushTravelAction)        
<a name="901"></a>            obj.beforeTravelNotifications(gDobj);
<a name="902"></a>        
<a name="903"></a>        return true;
<a name="904"></a>    }
<a name="905"></a>    
<a name="906"></a>    preCondOrder = 50
<a name="907"></a>;
<a name="908"></a>
<a name="909"></a>
<a name="910"></a>
<a name="911"></a>/* 
<a name="912"></a> *   A PreCondition to check whether an object counts aa currently in the actor's inventory (for the
<a name="913"></a> *   purposes of being dropped). 
<a name="914"></a> */
<a name="915"></a>objCarried: PreCondition
<a name="916"></a>    
<a name="917"></a>    verifyPreCondition(obj)
<a name="918"></a>    {
<a name="919"></a>        /* 
<a name="920"></a>         *   If the object is fixed in place it can't be picked up, so there's
<a name="921"></a>         *   no point in trying. BUT we also have to check for the case that the
<a name="922"></a>         *   object is directly in the player (perhaps because a body part).
<a name="923"></a>         */        
<a name="924"></a>        if(obj.isFixed &amp;&amp; !obj.isDirectlyIn(gActor))
<a name="925"></a>            illogical(obj.cannotTakeMsg);
<a name="926"></a>        
<a name="927"></a>        
<a name="928"></a>        /* 
<a name="929"></a>         *   If the actor isn't carrying the object it's not in the actor's inventory,
<a name="930"></a>         */        
<a name="931"></a>        else if(!obj.isIn(gActor))
<a name="932"></a>            illogicalNow(obj.notHoldingMsg);
<a name="933"></a>        
<a name="934"></a>        /*
<a name="935"></a>         *   If the object isn't directly in the actor and the object's location doesn't allow
<a name="936"></a>         *   objects to be dropoped from it, then it doesn't count as being in the actor's inventory
<a name="937"></a>         *   for the purpose of dropping.
<a name="938"></a>         */
<a name="939"></a>        else if(obj.location != gActor &amp;&amp; !obj.location.canDropContents)
<a name="940"></a>            illogicalNow(obj.notHoldingMsg);
<a name="941"></a>        
<a name="942"></a>        
<a name="943"></a>        
<a name="944"></a>        
<a name="945"></a>        else
<a name="946"></a>            logical;
<a name="947"></a>        
<a name="948"></a>        
<a name="949"></a>        
<a name="950"></a>    }
<a name="951"></a>    
<a name="952"></a>    checkPreCondition(obj, allowImplicit) 
<a name="953"></a>    { 
<a name="954"></a>        /* 
<a name="955"></a>         *   if the object is already held, we're already done.
<a name="956"></a>         */
<a name="957"></a>        if (obj == nil || obj.isDirectlyIn(gActor))
<a name="958"></a>            return true;
<a name="959"></a>        
<a name="960"></a>        
<a name="961"></a>        /* 
<a name="962"></a>         *   If we're allowed to attempt an implicit action, and obj's location allows it, try
<a name="963"></a>         *   taking obj implicitly and see if we succeed.
<a name="964"></a>         */
<a name="965"></a>        if(allowImplicit &amp;&amp; obj.location.canDropContents) 
<a name="966"></a>        {    
<a name="967"></a>            /* 
<a name="968"></a>             *   Try taking obj implicitly and note if we were allowed to make
<a name="969"></a>             *   the attempt.
<a name="970"></a>             */
<a name="971"></a>            local tried = tryImplicitAction(TakeFrom, obj, obj.location);
<a name="972"></a>            
<a name="973"></a>            /*  
<a name="974"></a>             *   If obj is now held by the actor return true to signal that this
<a name="975"></a>             *   precondition has now been met.
<a name="976"></a>             */
<a name="977"></a>            if(obj.isDirectlyIn(gActor))
<a name="978"></a>              return true;   
<a name="979"></a>            
<a name="980"></a>            /* 
<a name="981"></a>             *   Otherwise, if we tried but failed to take obj, return nil to
<a name="982"></a>             *   signal that this precondition can't be met (so the main action
<a name="983"></a>             *   cannot proceed). The attempt to take obj will have explained
<a name="984"></a>             *   why it failed, so there's no need for any further explanation
<a name="985"></a>             *   here.
<a name="986"></a>             */
<a name="987"></a>            if(tried)
<a name="988"></a>                return nil;            
<a name="989"></a>        }
<a name="990"></a>        
<a name="991"></a>        /* 
<a name="992"></a>         *   If we reach here obj isn't being held by the actor and we weren't
<a name="993"></a>         *   allowed to try to take it; display a message explaining the
<a name="994"></a>         *   problem.
<a name="995"></a>         */
<a name="996"></a>        gMessageParams(obj);
<a name="997"></a>        DMsg(not held, '{I} {am} not holding {the obj}. ');
<a name="998"></a>        
<a name="999"></a>        /* Then return nil to indicate that the precondition hasn't been met. */
<a name="1000"></a>        return nil;        
<a name="1001"></a>    }
<a name="1002"></a>;
</pre>
<div class=ftr>Adv3Lite Library Reference Manual<br>Generated on 25/04/2024 from adv3Lite version 2.0</div>
</body>
</html>
